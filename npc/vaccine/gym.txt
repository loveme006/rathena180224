//===== Hercules Script ======================================
//= Gym Pass Cash Item NPC
//===== By: ==================================================
//= Kisuka
//===== Current Version: =====================================
//= 1.3
//===== Description: =========================================
//= [Official Conversion]
//= Exchange Gym Passes to learn "Enlarge Weight Limit"
//= iRO NPC situated in Payon beside Kafra Shop.
//===== Additional Comments: =================================
//= 1.0 First version [L0ne_W0lf]
//= 1.1 Fixed the NPC saying you "00" [L0ne_W0lf]
//= 1.2 Replaced effect numerics with constants. [Samuray22]
//= 1.3 Updated script, and fixed potential errors. [L0ne_W0lf]
//============================================================

-	script	GymPass	-1,{
	mes "[헬스장 직원]";
	mes "어이 이봐. 요즘 사람들은 운동을 잘 안하는것 같아.";
	mes "너가 싸움을 할때 뿐 아니라 일상 생활에도";
	mes "튼튼한 몸은 필수라 이거지.";
	mes "내 말 알아듣겠어?";
	next;
	mes "[헬스장 직원]";
	mes "그래, 나와 함께 운동을 하지 않을래?";
	mes "몸이 튼튼해지면 들고 다닐수 있는 장비들도 더욱 많아질거야.";
	mes "하지만 운동을 하려면";
	mes "^FF0000헬스장 회원권^000000이 있어야 한다고!";
	next;
	mes "[헬스장 직원]";
	mes "그리고 안전을 위해서 헬스는 10회만";
	mes "가능하게 되있어.";
	mes "운동할 준비가 됐니?";
	next;
	switch(select("네:아니오:회원 서비스를 받고싶은데...")) {
	case 1:
		if (gympassmemory < 10) {
			//set .@add_carry,gympassmemory + 1;
			set .@remain_carry,10 - gympassmemory -1;
			if (countitem(7776) > 0) {
				mes "[헬스장 직원]";
				mes "오! 역시 운동을 좋아할줄 알았어!";
				mes "회원권도 가지고 있고!.";
				mes "좋아 운동을 시작해보자구!!";
				next;
				specialeffect2 EF_EARTHSPIKE;
				next;
				specialeffect2 EF_DEVIL;
				next;
				specialeffect2 EF_COIN;
				next;
				specialeffect2 EF_SIGHTRASHER;
				next;
				mes "[헬스장 직원]";
				mes "이제 몸도 건강해지고 소지량도 증가했을거야.";
				mes "앞으로 ^FF00000" + .@remain_carry + "^000000번 운동을 더 할 수 있어.";
				mes "운동을 하고 싶으면 언제든 찾아와!";
				delitem 7776,1; //Max_Weight_Up_Scroll
				set gympassmemory,gympassmemory+1;
				skill 681,gympassmemory,3;
				close;
			}
			else {
				mes "[헬스장 직원]";
				mes "흠.. 내가 말하지 않았나?";
				mes "운동을 하고 싶다면 ^FF0000헬스장 회원권^000000을 가져오라고!";
				close;
			}
		}
		else {
			mes "[헬스장 직원]";
			mes "내가 말했잖아. 운동은 10번만 가능하다니까?";
			mes "더 무리하게 운동했다간 몸이 상해버려...";
			mes "적당한 운동이 있으면 적당한 휴식도 필요한 법이야.";
			close;
		}
	case 2:
		mes "[헬스장 직원]";
		mes "이런...";
		mes "마음이 바뀌면 언제든지 와.";
		close;
	case 3:
		if (gympassmemory > 0) {
			mes "[헬스장 직원]";
			mes "무슨일 있었어?";
			mes "운동은 언제나 규칙적이게!";
			mes "걱정마! 저번처럼 트레이닝 해줄테니까!";
			next;
			specialeffect2 EF_EARTHSPIKE;
			next;
			specialeffect2 EF_DEVIL;
			next;
			specialeffect2 EF_COIN;
			next;
			specialeffect2 EF_SIGHTRASHER;
			next;
			mes "[헬스장 직원]";
			mes "어떄?";
			mes "이제 예전처럼 소지량도 증가했을거야.";
			mes "또 필요하면 언제든지 다시 오라구!";
			skill 681,gympassmemory,3;
			close;
		}
		else {
			mes "[헬스장 직원]";
			mes "흠...";
			mes "헬스장 회원이 아니면";
			mes "회원서비스를 받을 수 없어.";
			close;
		}
	}
}

auction_01,16,78,4	duplicate(GymPass)	헬스장 직원#bspayon	4_M_HUMAN_02
