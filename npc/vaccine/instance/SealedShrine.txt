//===== rAthena Script ======================================= 
//= Sealed Shrine
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 2.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Seal away the Great Demon Baphomet.
//===== Additional Comments: ================================= 
//= 1.0 First version. [L0ne_W0lf]
//= 1.1 Added setquest for instance cooldown. [L0ne_W0lf]
//= 1.2 Fixed Torch#12 not being disabled. (bugreport:4044) [L0ne_w0lf]
//= 1.3 Fixed cutins not being removed. (bugreport:4117) [L0ne_w0lf]
//= 1.4 Fixed some typos. [Gepard]
//= 1.5 Fixed not being able to enter dungeon for the first two hours.
//= 1.6 Players should be able to re-take the quest after 12 hours now.
//=     Changed quest 3046 to 3040. This is the correct quest id.
//=     Removed checking quest 3045, unofficial check.
//= 1.7 Fixed a checkquest condition never setting quest. [L0ne_W0lf]
//= 1.8 Added 'instance_check_party' command to the script for proper checking if
//=     the invoking party meets the requirements to join the Memorial Dungeon.
//= 1.9 Fixed broken else/if chaining. [Joseph]
//= 2.0 Some optimization. [Euphy]
//= 2.1 Fixed quest being removed before the 12 hours cool down. [Joseph]
//= 2.2 Removed extra setquest (quest id:3040).	[Joseph]
//= 2.3 Instance system rewrite. [Euphy]
//============================================================ 

// Sealed Catacomb Entrance
//============================================================
monk_test,309,146,3	script	항마수사 패트릭#edq	412,{
	cutin "ins_cata_pri_n",2;
	mes "[항마수사 패트릭]";
	mes "이 세상의 평화는 영원히 지속될 수 없다 ... 악의 손이 세상에 다시 오고 있으니 ...";
	next;
	mes "[항마수사 패트릭]";
	mes "이 곳에 온 이유가 무엇이냐?";
	next;
	if (countitem(6004) > 0) {
		set .@doll,1;
		set .@selection,select("여기는 어디죠?:들어가고싶다.:저주받은 바포메트 인형에 대해...:취소");
	}
	else {
		set .@selection,select("여기는 어디죠?:들어가고싶다.:취소.");
	}
	switch(.@selection) {
	case 1:
		mes "[항마수사 패트릭]";
		mes "음... 모르나? 여기는 항마수사가 되고싶은 형제들이 훈련하고 기도하는 세인트 캐피톨리나 수도원이다.";
		next;
		mes "[항마수사 패트릭]";
		mes "그리고 이곳은... 뭐랄까..? 그래, 악마의 무덤이다. 무덤...";
		next;
		mes "[항마수사 패트릭]";
		mes "한 때 이 세상을 파괴한 엄청난 악마가 자고있는 곳이지.";
		next;
		switch(select("더 얘기 해주세요.:그만 듣는다.")) {
		case 1:
			cutin "ins_cata_pri_n",2;
			mes "[항마수사 패트릭]";
			mes "바포메트...가 이 악마의 이름이지... 나는 그의 이름을 들어본적이 있다.";
			next;
			mes "[항마수사 패트릭]";
			mes "수많은 용감한 형제들이 이 수도원에서 훈련했지...";
			next;
			mes "[항마수사 패트릭]";
			mes "그리고 너의 앞에 있는 무덤에... 바포메트를 봉인했다.";
			next;
			mes "[항마수사 패트릭]";
			mes "하지만... 우리는 마신 모로크가 세상을 지배하기 위해 그의 힘을 발휘하려는 것을 안다.";
			next;
			mes "[항마수사 패트릭]";
			mes "바포메트도... 마신 모로크가 인장의 힘을 약화시켜 깨어났고, 다시 세계를 지배하기 위한 준비를 하고있다.";
			next;
			mes "[항마수사 패트릭]";
			mes "나는 바포메트를 신전에 봉인할 용감한 사람을 찾고있다... 우리가 예전에 했던 것처럼...";
			next;
			mes "[항마수사 패트릭]";
			mes "더 많이 훈련하여 악으로부터 세계를 보호하기 위해 힘을 사용하거라...";
			break;
		case 2:
			mes "[항마수사 패트릭]";
			mes "나는 바포메트를 신전에 봉인할 용감한 사람을 찾고있다... 우리가 예전에 했던 것처럼...";
			next;
			mes "[항마수사 패트릭]";
			mes "더 많이 훈련하여 악으로부터 세계를 보호하기 위해 힘을 사용하거라...";
			break;
		}
		break;
	case 2:
		if (BaseLevel >= 75) {

			set .@party_id,getcharid(1);

			mes "[항마수사 패트릭]";
			mes "너가 신전으로 가서 바포메트를 봉인하겠다는 것이냐?";
			next;

			// 12 hour cooldown
			set .@ins_bapho_check,checkquest(3040,PLAYTIME);
			// Two hour play limit
			//set .@ins_bapho_check2,checkquest(3045,PLAYTIME);

			if (.@ins_bapho_check == -1) {
				if (getpartyleader(.@party_id,2) == getcharid(0) && instance_check_party(.@party_id,2,75)) {
					mes "[항마수사 패트릭]";
					mes "파티 이름은 "+getpartyname(.@party_id)+"...";
					mes "리더 이름은 "+strcharinfo(0)+"...";
					if (instance_create("Sealed Catacomb") < 0) {
						mes "음... 문제가 있는 것 같군. 내가 조사해보고 오지.";
					}
					else {
						mes "그래...너의 파티가 들어갈 수 있도록 해주지.";
						next;
						mes "[항마수사 패트릭]";
						mes "인장이 끊어지면 신호가 보일 것이다. 신호가 나타날 때까지 기다려라.";
						next;
						mes "[항마수사 패트릭]";
						mes "신호가 보이면 무덤에 손을 올려라... 그러면 안으로 들어갈 수 있다.";
						next;
						mes "[항마수사 패트릭]";
						mes "한 가지 기억해야 할 것은... 이 신전에 들어가는 사람은 모두 바포메트의 저주를 받고, 저주에 걸린 동안에는 들어가거나 나갈 수 없다.";
						next;
						mes "[항마수사 패트릭]";
						mes "그리고 한 가지 더... 이 저주받은 지역은 인장의 효과로 외부의 힘에 의해 보호되는 기술이 금지된다.";
						next;
						mes "[항마수사 패트릭]";
						mes "예를 들어, ^0000FF세이프티 월과 아슘프티오^000000같은 기술... 그래서 신전에 들어가기 전이 준비하는 것이 좋다.";
					}
				}
				else {
					mes "[항마수사 패트릭]";
					mes "음... 네 담력은 알겠지만... 이 곳에 아무나 들어갈 수 없네. 파티의 리더만이 먼저 들어갈 수 있다.";
					next;
					mes "[항마수사 패트릭]";
					mes "파티의 리더가 먼저 들어가면 나머지도 들어갈 수 있다. 이 것이 수도원이 규칙이니 이해하도록...";
				}
			}
			else if (.@ins_bapho_check == 0 || .@ins_bapho_check == 1) {
				mes "[항마수사 패트릭]";
				mes "최근에 이 신전에 입장 한 것 같구만 ... 바포메트의 저주가 남아있기 때문에 다시 입장 할 수 없다. 바포메트의 저주는 일정 시간이 지나면 사라진다.";
			}
			else if (.@ins_bapho_check == 2) {
				mes "[항마수사 패트릭]";
				mes "음... 바포메트의 저주가 약해보이네. 나는 그것을 없앨 수 있지.";
				next;
				mes "[항마수사 패트릭]";
				mes "하압!!!!!!";
				next;
				erasequest 3040;
				if (checkquest(3041)>=0) erasequest 3041;
				if (checkquest(3045)>=0) erasequest 3045;
				mes "[항마수사 패트릭]";
				mes "후... 이제 끝났네. 바포메트의 저주를 풀어 놓았으니 다시 들어갈 수 있을거야.";
			}
		}
		else {
			mes "[항마수사 패트릭]";
			mes "음... 이 장소는 위험하니 들어가기 위해 더 많이 훈련해야한다... 적어도 75 레벨은 돼야 여기에 들어갈 수 있다.";
			next;
			mes "[항마수사 패트릭]";
			mes "더 많이 훈련하고 다시 오거라.";
		}
		break;
	case 3:
		if (.@doll == 1) {
			mes "[항마수사 패트릭]";
			mes "그건... 너가 들고있는 불길한 인형... 그것 좀 보자.";
			next;
			mes "[항마수사 패트릭]";
			mes "... ... ...";
			next;
			mes "[항마수사 패트릭]";
			mes "역시 맞았어. 여기서 바포메트의 악이 느껴진다. 그래서, 인형으로 무엇을 할 것이냐?";
			next;
			mes "[항마수사 패트릭]";
			mes "아무것도 하지 않으면 이건 위험하고 쓸모없는 것이다... 너를 도와줄 수 있는 사람을 소개해주겠다... 만나보겠나?";
			next;
			mes "[항마수사 패트릭]";
			mes "수도원 본관 근처에 있는 ^0000FF녹슨 검은 손^000000을 만나러 가보게... 그가 이 인형을 쓸모있게 만들어줄거야.";
			setquest 3042;
			break;
		}
	case 4:
		mes "[항마수사 패트릭]";
		mes "나는 바포메트를 신전에 봉인할 용감한 사람을 찾고있다... 우리가 예전에 했던 것처럼...";
		next;
		mes "[항마수사 패트릭]";
		mes "더 많이 훈련하여 악으로부터 세계를 보호하기 위해 힘을 사용하거라...";
		break;
	}
	close2;
	cutin "",255;
	end;
}

monk_test,306,151,3	script	바포메트의 무덤#edq	111,{
	if (countitem(6002)) delitem 6002,countitem(6002); //Token_Of_Apostle
	mes "이 묘비에는 큰 뿔이 있는 사악한 악마의 조각이 있습니다.";
	next;
	if(select("묘비를 만진다.:되돌아간다.") == 2)
		close;

	// 12 hour cooldown
	set .@ins_bapho_check,checkquest(3040,PLAYTIME);
	// Two hour play limit
	//set .@ins_bapho_check2,checkquest(3045,PLAYTIME);

	if (.@ins_bapho_check == -1) {
		switch(instance_enter("Sealed Catacomb")) {
		case IE_OTHER:
		case IE_NOINSTANCE:
			mes "묘비가 차갑다. 아무 일도 일어나지 않았다.";
			close;
		case IE_NOMEMBER:
			mes "[항마수사 패트릭]";
			mes "위험한 장소에 들어가려면 혼자 갈 수 없다. 파티에 가입해서 다시 오거라.";
			close;
		case IE_OK:
			mapannounce "monk_test","[" + getpartyname(.@party_id) + "]의 맴버 [" + strcharinfo(0) + "]가 봉인의 신전에 입장하였습니다.",bc_map,"0x00ff99";
			setquest 3040;
			//warp "1@cata",100,224;
			close;
		}
	} else if (.@ins_bapho_check == 0 || .@ins_bapho_check == 1) {
		mes "[항마수사 패트릭]";
		mes "최근에 이 신전에 입장 한 것 같구만 ... 바포메트의 저주가 남아있기 때문에 다시 입장 할 수 없다. 바포메트의 저주는 일정 시간이 지나면 사라진다.";
		close;
	} else if (.@ins_bapho_check == 2) {
		mes "[항마수사 패트릭]";
		mes "음... 바포메트의 저주가 약해보이네. 나는 그것을 없앨 수 있지.";
		next;
		mes "[항마수사 패트릭]";
		mes "하압!!!!!";
		specialeffect2 EF_HOLYHIT;
		erasequest 3040;
		if (checkquest(3041)>=0) erasequest 3041;
		if (checkquest(3045)>=0) erasequest 3045;
		next;
		mes "[항마수사 패트릭]";
		mes "후... 이제 끝났네. 바포메트의 저주를 풀어 놓았으니 다시 들어갈 수 있을거야.";
		close;
	}
}

// Sealed Shrine Interior
//============================================================
1@cata,1,2,0	script	ins_baphomet_lotto	-1,{
OnInstanceInit:
	// Pick one "true" Gravestone and 12 "false" Gravestones.
	set .@true, rand(1,13);
	for(set .@i,1; .@i<=13; set .@i,.@i+1) {
		disablenpc instance_npcname("Gravestone#1F_"+.@i+((.@i == .@true)?"T":"F"));
	}
	disablenpc instance_npcname("ins_baphomet_lotto");
	end;
}

1@cata,3,2,0	script	ins_baphomet_lotto2	-1,{
OnEnable:
	for(set .@i,1; .@i<=12; set .@i,.@i+1)
		enablenpc instance_npcname("Bobbing Torch#"+.@i);
	end;
}

1@cata,4,2,0	script	ins_baphomet_lotto3	-1,{
OnEnable:
	set .@label$, instance_npcname("ins_baphomet_lotto3")+"::OnMyMobDead";
	set .@map$, instance_mapname("1@cata");
	monster .@map$,0,0,"바포메트의 사도",1869,1,.@label$;
	monster .@map$,0,0,"바포메트의 사도",1291,1,.@label$;
	monster .@map$,0,0,"바포메트의 사도",1869,1,.@label$;
	monster .@map$,0,0,"바포메트의 사도",1291,1,.@label$;
	monster .@map$,0,0,"바포메트의 사도",1869,1,.@label$;
	monster .@map$,0,0,"바포메트의 사도",1291,1,.@label$;
	monster .@map$,0,0,"바포메트의 사도",1869,1,.@label$;
	monster .@map$,0,0,"바포메트의 사도",1132,1,.@label$;
	monster .@map$,0,0,"바포메트의 사도",1117,1,.@label$;
	monster .@map$,0,0,"바포메트의 사도",1132,1,.@label$;
	monster .@map$,0,0,"바포메트의 사도",1117,1,.@label$;
	monster .@map$,0,0,"바포메트의 사도",1132,1,.@label$;
	monster .@map$,0,0,"바포메트의 사도",1291,1,.@label$;
	monster .@map$,0,0,"바포메트의 사도",1117,1,.@label$;
	monster .@map$,0,0,"바포메트의 사도",1869,1,.@label$;
	end;

OnDisable:
	killmonster instance_mapname("1@cata"),instance_npcname("ins_baphomet_lotto3")+"::OnMyMobDead";
	end;

OnMyMobDead:
	set .@map$, instance_mapname("1@cata");
	if (mobcount(.@map$,instance_npcname("ins_baphomet_lotto3")+"::OnMyMobDead") < 1) {
		mapannounce .@map$, "모든 바포메트의 사도가 죽었다!",bc_map,"0x00ff99";
	}
	getitem 6002,1; //Token_Of_Apostle
	end;
}

// Soul of hero near the north grave
//============================================================
1@cata,141,221,0	script	Gravestone#	844,3,3,{
	set .@party_id,getcharid(1);
	if ('ins_baphomet == 0) {
		mes "묘비가 떨린다...";
		next;
		mes "묘비를 만졌을 때, 나는 목소리를 들었다.";
		next;
		specialeffect2 EF_CONE;
		mes "[묘비의 목소리]";
		mes "나는 너같은 모험가가 여기에 오는 것을 기다리고 있었다...";
		next;
		while(1) {
			switch(select("누구세요?:나를 기다렸다고?:취소.")) {
			case 1:
				mes "[묘비의 목소리]";
				mes "나는 바포메트를 멈추려는 전사 중 한명이였다. 이미 나는 죽어서 영혼만 남아있지...";
				next;
				mes "[묘비의 목소리]";
				mes "알고있겠지만... 우리에게는 시간이 별로 없다. 혼자서는 바포메트를 막을 수 없어. 수년에 걸쳐 악마의 힘은 강해졌다.";
				next;
				mes "[묘비의 목소리]";
				mes "과거에, 나와 동료들은 지하 2층에 위치한 제단에서 바포메트를 봉인하고 입구를 막았지.";
				next;
				mes "[묘비의 목소리]";
				mes "나는 영혼을 펜던트로 옮겼고, 그래서 이 세상에 남아있을 수 있었지. 그 때 나는 이 무덤의 수호자가 되었다.";
				next;
				break;
			case 2:
				mes "[묘비의 목소리]";
				mes "바포메트의 힘이 우리가 만든 인장을 부술거야. 재활성 시키지 않으면 바포메트의 부활은 시간문제일 뿐이야.";
				next;
				mes "[묘비의 목소리]";
				mes "입구를 열려면 내 영혼을 구체화 해야해. 내가 구체화되면 입구를 열고 약화된 인장을 다시 활성시킬거야.";
				next;
				mes "[묘비의 목소리]";
				mes "나를 구체화하려면 내 펜던트를 찾아야해. 무덤 근처에서 찾을 수 있을거야.";
				next;
				mes "[묘비의 목소리]";
				mes "^0000FF파티의 리더^000000가 펜던트를 가져오면, 내 영혼이 구체화될 수 있어. 서둘러.";
				set 'ins_baphomet,1;
				close;
			case 3:
				mes "목소리가 희미 해지는 것을 느낄 수 있다.";
				close;
			}
		}
	}
	else if ('ins_baphomet == 1) {
		mes "[묘비의 목소리]";
		mes "입구를 열려면 내 영혼을 구체화 해야해. 내가 구체화되면 입구를 열고 약화된 인장을 다시 활성시킬거야.";
		next;
		mes "[묘비의 목소리]";
		mes "나를 구체화하려면 내 펜던트를 찾아야해. 무덤 근처에서 찾을 수 있을거야.";
		next;
		mes "[묘비의 목소리]";
		mes "^0000FF파티의 리더^000000가 펜던트를 가져오면, 내 영혼이 구체화될 수 있어. 서둘러.";
		close;
	}
	else if (('ins_baphomet == 2) && (getpartyleader(.@party_id,2) == getcharid(0))) {
		mes "[묘비의 목소리]";
		mes "펜던트를 찾았어?";
		next;
		if (countitem(6003) > 0) {
			specialeffect EF_MAPPILLAR;
			mes "[묘비의 목소리]";
			mes "그래...이게... 내 펜던트야.";
			next;
			delitem 6003,1; //Soul_Pendant
			enablenpc instance_npcname("Ancient Hero's Soul#1F");
			disablenpc instance_npcname("Gravestone#");
			mes "[묘비의 목소리]";
			mes "이제 내 영혼을 구체화할 수 있어. 이 무덤 중심에 있는 불의 제단 앞에서 기다릴게. 거기서 만나자.";
			next;
			mapannounce instance_mapname("1@cata"), "고대 영웅의 영혼: 불의 제단 앞에서 기다릴게.",bc_map,"0xFFFF00";
			mes "목소리가 희미 해지는 것이 느껴진다.";
			close;
		}
		else {
			mes "[묘비의 목소리]";
			mes "아직도 거깄어? 빨리 내 펜던트를 가져와.";
			next;
			mes "[묘비의 목소리]";
			mes "무덤 근처에서 찾을 수 있을거야.";
			close;
		}
	}
	else {
		mes "[묘비의 목소리]";
		mes "^0000FF파티장^000000과 얘기하고 싶어.";
		close;
	}
	end;

OnTouch:
	if ('ins_baphomet == 0) {
		mes "'Krrrr... Krrrr...'";
		next;
		mes "무덤에서 이상한 것이 느껴진다. 누군가가 부르는 것처럼...";
		close;
	}
	end;

OnInstanceInit: // Temporary fix for @reloadscript.
	set 'ins_baphomet,0;
	end;
}

1@cata,176,119,4	script	Ancient Hero's Soul#1F	411,{
	set .@party_id,getcharid(1);
	cutin "ins_cata_champ_n",2;
	if ('ins_baphomet == 2) {
		mes "[고대 영웅의 영혼]";
		mes "With your help, my soul can be substantialized. I want to talk more, but we do not have enough time...";
		next;
		mes "[고대 영웅의 영혼]";
		mes "I must be substantialized within the next hour. To reach the Main Altar underground, you must help me perform the ceremony for opening each seal.";
		next;
		mes "[고대 영웅의 영혼]";
		mes "Now I'll tell you what should you do. First, collect ^0000FFEssence of Fire^000000 from the torches on the graves...";
		next;
		mes "[고대 영웅의 영혼]";
		mes "Second, all members who will enter the underground must carry a symbol, called the Token of Apostle.";
		next;
		while(1) {
			cutin "ins_cata_champ_n",2;
			switch(select("What is Essence of Fire?:What is a Token of Apostle?:What should I do?:I am ready.")) {
			case 1:
				set .@ins_baphomet_1f_1,.@ins_baphomet_1f_1+1;
				mes "[Ancient Hero's Soul]";
				mes "You can see the torches here and there. These torches are the essence of Thor, the god of Thunder. They are inherited from our predecessors to stop the darkness of Baphomet...";
				next;
				mes "[Ancient Hero's Soul]";
				mes "To open the sealed underground gate, I must be purified by the ^0000FFEssence of Fire^000000 which has the power of Thor. Collect ^0000FF10 Essence of Fire^000000 from the torches.";
				next;
				mes "[Ancient Hero's Soul]";
				mes "One thing you must remember is... ^0000FFEssence of Fire^000000 can only be collected by the Inheritor of Faith.";
				next;
				mes "[Ancient Hero's Soul]";
				mes "I'll give the token of the Inheritor of Faith to the party leader. Only the party leader can collect the ^0000FFEssence of Fire^000000.";
				next;
				break;
			case 2:
				set .@ins_baphomet_1f_2,.@ins_baphomet_1f_2+1;
				mes "[Ancient Hero's Soul]";
				mes "At that time, it was impossible to get rid of Devil Baphomet by ourselves. After numerous heroes sacrificed their lives, we could barely seal him under this Abbey.";
				next;
				mes "[Ancient Hero's Soul]";
				mes "However, Baphomet never gave up. He continuously strengthened his power. And his power has brought new life to this shrine.";
				next;
				mes "[Ancient Hero's Soul]";
				mes "Some of these monsters have magical powers. Baphomet calls them his 'Apostles'.";
				next;
				mes "[Ancient Hero's Soul]";
				mes "The apostles are cloning themselves to fill the shrine with their evil energy. They are the monsters here in this catacomb.";
				next;
				mes "[Ancient Hero's Soul]";
				mes "Kill the Apostles. They can only be distinguished from their clones because they will possess a ^0000FFToken of Apostle^000000...";
				next;
				mes "[Ancient Hero's Soul]";
				mes "If each party member possesses a ^0000FFToken of Apostle^000000, Baphomet will not be able to perceive your entrance to the Main Altar underground.";
				next;
				mes "[Ancient Hero's Soul]";
				mes "If Baphomet perceives you when you pass through the sealed gate, he may release his tremendous magical power. Then, this abbey will be demolished.";
				next;
				mes "[Ancient Hero's Soul]";
				mes "Kill the apostles and get the ^0000FFToken of Apostle^000000.";
				next;
				break;
			case 3:
				set .@ins_baphomet_1f_3,.@ins_baphomet_1f_3+1;
				if (getpartyleader(.@party_id,2) == getcharid(0)) {
					mes "[Ancient Hero's Soul]";
					mes "You look like the leader of this party. You need to go and get ^0000FF10 Essence of Fire^000000 from the torches.";
					next;
					mes "[Ancient Hero's Soul]";
					mes "Now I'll carve you the symbol which shows you're a inheritor of faith. Be aware that no one in your party but you can collect the Essence of Fire.";
					next;
					specialeffect2 EF_HOLYHIT;
					mes "[Ancient Hero's Soul]";
					mes "You should also carry a ^0000FFToken of Apostle^000000, so find one for yourself too.";
					next;
					mes "[Ancient Hero's Soul]";
					mes "When all of you are ready to go, let me know. I'll open the sealed gate when you're ready.";
					next;
				}
				else {
					mes "[Ancient Hero's Soul]";
					mes "Make sure that your party leader has listened to my explanation of what you and your companions must do.";
					next;
					mes "[Ancient Hero's Soul]";
					mes "Kill the Apostles of Baphomet and find their ^0000FFToken of Apostle^000000.";
					next;
					mes "[Ancient Hero's Soul]";
					mes "It might be better to kill all of them because there is no way to differentiate them from their clones.";
					next;
					mes "[Ancient Hero's Soul]";
					mes "Are you ready?";
					mes "Make sure that you have listened to all that I have to say before saying that you are ready.";
					next;
				}
				break;
			case 4:
				if ((.@ins_baphomet_1f_1 > 0) && (.@ins_baphomet_1f_2 > 0) && (.@ins_baphomet_1f_3 > 0)) {
					cutin "ins_cata_champ_n",2;
					mes "[Ancient Hero's Soul]";
					mes "Are you ready to go? Then I'll open this sealed gate now.";
					next;
					set .@exitloop,1;
				}
				else {
					cutin "ins_cata_champ_a",2;
					mes "[Ancient Hero's Soul]";
					mes "It may be difficult but I wish you luck braving the perils of this catacomb.";
					next;
					mes "[Ancient Hero's Soul]";
					mes "Remember, you need to collect ^0000FF10 Essence of Fire^000000 from the torches and the ^0000FFToken of Apostle^000000 from the Apostles of Baphomet.";
					next;
				}
				break;
			}
			if (.@exitloop) break;
		}
		if (getpartyleader(.@party_id,2) == getcharid(0)) {
			mes "[Ancient Hero's Soul]";
			mes "To remind you again, I must be substantialized within the next hour. So everyone, finish your work within that time!";
			set 'ins_baphomet,3;
			donpcevent instance_npcname("ins_baphomet_1f_timer")+"::OnEnable";
			donpcevent instance_npcname("ins_baphomet_lotto2")+"::OnEnable";
			donpcevent instance_npcname("ins_baphomet_lotto3")+"::OnEnable";
		}
		else {
			mes "[Ancient Hero's Soul]";
			mes "To remind you again, I can be substantialized for the next hour. So everyone, finish your work within that time.";
			//set 'ins_baphomet,3;
		}
		close2;
		cutin "",255;
		end;
	}
	else if (('ins_baphomet == 3) && (getpartyleader(.@party_id,2) == getcharid(0))) {
		cutin "ins_cata_champ_n",2;
		mes "[Ancient Hero's Soul]";
		mes "Did you get 10 ^0000FFEssence of Fire^000000 and ^0000FFToken of Apostle^000000?";
		next;
		if ((countitem(6001) > 9) && (countitem(6002) > 0)) {
			delitem 6001,countitem(6001); //Essence_Of_Fire
			set 'ins_baphomet,4;
			mes "[Ancient Hero's Soul]";
			mes "Okay. You've done your work. Now check your companions and tell me when everyone has finished their work.";
		}
		else {
			mes "[Ancient Hero's Soul]";
			mes "Not ready yet? You should prepare 10 ^0000FFEssence of Fire^000000 and ^0000FFToken of Apostle^000000.";
		}
		close2;
		cutin "",255;
		end;
	}
	else if ('ins_baphomet == 3) {
		cutin "ins_cata_champ_n",2;
		mes "[Ancient Hero's Soul]";
		mes "Do you have a ^0000FFToken of Apostle^000000?";
		next;
		if (countitem(6002) > 0) {
			set 'ins_baphomet,4;
			mes "[Ancient Hero's Soul]";
			mes "Okay. You've done your work. Tell your representative to check your companions and come to me when everyone has finished their work.";
		}
		else {
			mes "[Ancient Hero's Soul]";
			mes "Not ready yet? You should prepare ^0000FFToken of Apostle^000000.";
		}
		close2;
		cutin "",255;
		end;
	}
	else if (('ins_baphomet == 4) && (getpartyleader(.@party_id,2) == getcharid(0))) {
		cutin "ins_cata_champ_n",2;
		mes "[Ancient Hero's Soul]";
		mes "Are you ready? I opened the sealed gate. To pass the gate, you should carry a ^0000FFToken of Apostle^000000.";
		next;
		set 'ins_baphomet,5;
		specialeffect EF_TELEPORTATION;
		enablenpc instance_npcname("ins_bapho_to_2f");
		mes "[Ancient Hero's Soul]";
		mes "Now you can go to the main altar. It is located in the bottom right corner of this floor.";
		next;
		mes "[Ancient Hero's Soul]";
		mes "Your real battle will begin... I'll follow you soon and find a way to help you.";
		next;
		mes "[Ancient Hero's Soul]";
		mes "Go ahead, warriors.";
		cutin "",255;
		mapannounce instance_mapname("1@cata"), "Ancient Hero's Soul : Now you can go to the Main Altar's gate. It is located in the Southeast",bc_map,"0xFFFF00";
		close;
	}
	else if ('ins_baphomet == 4) {
		cutin "ins_cata_champ_n",2;
		mes "[Ancient Hero's Soul]";
		mes "Are you ready? I opened the sealed gate. To pass the gate, you should carry a ^0000FFToken of Apostle^000000.";
		next;
		mes "[Ancient Hero's Soul]";
		mes "I'll complete opening the sealed gate when your representative tells me that you're ready.";
		cutin "",255;
		close;
	}
	else if ('ins_baphomet == 5) {
		cutin "ins_cata_champ_n",2;
		mes "[Ancient Hero's Soul]";
		mes "What are you doing? The entrance of the main altar is opened now, go and fight! The entrance is near the bottom right side of this floor.";
		cutin "",255;
		close;
	}
	else {
		cutin "ins_cata_champ_n",2;
		mes "[Ancient Hero's Soul]";
		mes "I have nothing to say to you...";
		cutin "",255;
		close;
	}

OnInstanceInit:
	disablenpc instance_npcname("Ancient Hero's Soul#1F");
	end;
}

// To 2F Warp
//============================================================
1@cata,281,12,0	script	ins_bapho_to_2f	45,1,1,{
OnTouch:
	if (countitem(6002) > 0) {
		delitem 6002,countitem(6002); //Token_Of_Apostle
		set 'ins_baphomet,5;
		warp instance_mapname("2@cata"),80,144;
		end;
	}
	else {
		mes "[고대 영웅의 영혼]";
		mes "사도의 징표는 어디있지? 이 문을 통과하기 위해서 사도의 징표를 가져와야한다고.";
		close;
	}

OnInstanceInit:
	disablenpc instance_npcname("ins_bapho_to_2f");
	end;
}

// Gravestones
//============================================================
-	script	Gravestone#SS1	-1,{
	if ('ins_baphomet == 1) {
		getitem 6003,1; //Soul_Pendant
		set 'ins_baphomet,2;
		mes "기울어져있는 무덤 아래에서 작은 물체가 빛난다.";
		next;
		mes "[" + strcharinfo(0) + "]";
		mes "펜던트인가...";
		close;
	}
	else {
		mes "이 무덤에서는 우울함만 느껴진다.";
		close;
	}
}
1@cata,86,214,0	duplicate(Gravestone#SS1)	Gravestone#1F_1T	844
1@cata,200,209,0	duplicate(Gravestone#SS1)	Gravestone#1F_2T	844
1@cata,230,195,0	duplicate(Gravestone#SS1)	Gravestone#1F_3T	844
1@cata,118,182,0	duplicate(Gravestone#SS1)	Gravestone#1F_4T	844
1@cata,193,182,0	duplicate(Gravestone#SS1)	Gravestone#1F_5T	844
1@cata,253,156,0	duplicate(Gravestone#SS1)	Gravestone#1F_6T	844
1@cata,88,154,0	duplicate(Gravestone#SS1)	Gravestone#1F_7T	844
1@cata,127,116,0	duplicate(Gravestone#SS1)	Gravestone#1F_8T	844
1@cata,90,98,0	duplicate(Gravestone#SS1)	Gravestone#1F_9T	844
1@cata,188,84,0	duplicate(Gravestone#SS1)	Gravestone#1F_10T	844
1@cata,244,42,0	duplicate(Gravestone#SS1)	Gravestone#1F_11T	844
1@cata,127,32,0	duplicate(Gravestone#SS1)	Gravestone#1F_12T	844
1@cata,267,30,0	duplicate(Gravestone#SS1)	Gravestone#1F_13T	844

-	script	Gravestone#SS2	-1,{
	mes "이 무덤에서는 우울함만 느껴진다.";
	close;
}
1@cata,86,214,0	duplicate(Gravestone#SS2)	Gravestone#1F_1F	844
1@cata,200,209,0	duplicate(Gravestone#SS2)	Gravestone#1F_2F	844
1@cata,230,195,0	duplicate(Gravestone#SS2)	Gravestone#1F_3F	844
1@cata,118,182,0	duplicate(Gravestone#SS2)	Gravestone#1F_4F	844
1@cata,193,182,0	duplicate(Gravestone#SS2)	Gravestone#1F_5F	844
1@cata,253,156,0	duplicate(Gravestone#SS2)	Gravestone#1F_6F	844
1@cata,88,154,0	duplicate(Gravestone#SS2)	Gravestone#1F_7F	844
1@cata,127,116,0	duplicate(Gravestone#SS2)	Gravestone#1F_8F	844
1@cata,90,98,0	duplicate(Gravestone#SS2)	Gravestone#1F_9F	844
1@cata,188,84,0	duplicate(Gravestone#SS2)	Gravestone#1F_10F	844
1@cata,244,42,0	duplicate(Gravestone#SS2)	Gravestone#1F_11F	844
1@cata,127,32,0	duplicate(Gravestone#SS2)	Gravestone#1F_12F	844
1@cata,267,30,0	duplicate(Gravestone#SS2)	Gravestone#1F_13F	844

// Bobbing Torches
//============================================================
-	script	Bobbing Torch#SS	-1,{
	set .@party_id,getcharid(1);
	if (getpartyleader(.@party_id,2) == getcharid(0)) {
		if (('ins_baphomet == 3) && (countitem(6001) < 11)) {
			mes "모든 것을 태울 수 있을 것처럼 보이는 거대한 횃불이 내 앞에서 위아래로 움직인다.";
			next;
			mes "불꽃의 웅장한 모습과 열기가 나를 뒷걸음치게 하지만... 용기를 내어 횃불을 잡기위해 손을 뻗었다.";
			next;
			specialeffect2 EF_HOLYHIT;
			getitem 6001,1; //Essence_Of_Fire
			mes "상속자의 상징이 빛난다. 그리고 작은 크리스탈이 내 손에 떨어진다.";
			disablenpc instance_npcname(strnpcinfo(0));
			close;
		}
		else if (('ins_baphomet == 3) && (countitem(6001) > 10)) {
			mes "이미 10개의 불의 정수가 있으니, 더 이상 수집할 필요가 없다.";
			close;
		}
		else {
			mes "더 이상 불의 정수를 모을 필요가 없다.";
			close;
		}
	}
	else {
		mes "모든 것을 태울 수 있을 것처럼 보이는 거대한 횃불이 내 앞에서 위아래로 움직인다.";
		next;
		mes "[알 수 없는 목소리]";
		mes "당신은 믿음의 상속자가 아닙니다. 불의의 손으로 불의 본질을 모독하지 마십시오.";
		specialeffect2 EF_FIRESPLASHHIT;
		percentheal -50,0;
		close;
	}

OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
}
1@cata,267,210,0	duplicate(Bobbing Torch#SS)	Bobbing Torch#1	844
1@cata,85,182,0	duplicate(Bobbing Torch#SS)	Bobbing Torch#2	844
1@cata,267,154,0	duplicate(Bobbing Torch#SS)	Bobbing Torch#3	844
1@cata,157,139,0	duplicate(Bobbing Torch#SS)	Bobbing Torch#4	844
1@cata,193,138,0	duplicate(Bobbing Torch#SS)	Bobbing Torch#5	844
1@cata,193,102,0	duplicate(Bobbing Torch#SS)	Bobbing Torch#6	844
1@cata,157,102,0	duplicate(Bobbing Torch#SS)	Bobbing Torch#7	844
1@cata,113,112,0	duplicate(Bobbing Torch#SS)	Bobbing Torch#8	844
1@cata,253,56,0	duplicate(Bobbing Torch#SS)	Bobbing Torch#9	844
1@cata,239,56,0	duplicate(Bobbing Torch#SS)	Bobbing Torch#10	844
1@cata,71,42,0	duplicate(Bobbing Torch#SS)	Bobbing Torch#11	844
1@cata,155,14,0	duplicate(Bobbing Torch#SS)	Bobbing Torch#12	844

1@cata,1,1,0	script	ins_baphomet_1f_timer	-1,{
OnInstanceInit:
	disablenpc instance_npcname("ins_baphomet_1f_timer");
	end;

OnEnable:
	enablenpc instance_npcname("ins_baphomet_1f_timer");
	initnpctimer;
	end;

OnDisable:
	disablenpc instance_npcname("ins_baphomet_1f_timer");
	stopnpctimer;
	end;

OnTimer1800000:
	mapannounce instance_mapname("1@cata"), "고대 영웅의 영혼 : 시간이 없어! 서둘러!",bc_map,"0xFFFF00";
	end;

OnTimer2400000:
	mapannounce instance_mapname("1@cata"), "고대 영웅의 영혼 : 내 몸이 사라지고 있어... 서둘러!",bc_map,"0xFFFF00";
	end;

OnTimer3000000:
	mapannounce instance_mapname("1@cata"), "고대 영웅의 영혼 : 모두 끝났어... 다른 방법이 없지만 다음 기회를 기다리자...",bc_map,"0xFFFF00";
	end;

OnTimer3050000:
	mapannounce instance_mapname("1@cata"), "고대 영웅의 영혼 : 우린 실패했어... 하지만... 아직 기회가 있어. 다음엔 더 훈련해서 오길 바래...",bc_map,"0xFFFF00";
	end;

OnTimer3100000:
	mapannounce instance_mapname("1@cata"), "제단의 봉인을 푸는 데 실패하였습니다.",bc_map,"0xFFFF00";
	end;

OnTimer3500000:
	instance_warpall "monk_test",310,150;
	stopnpctimer;
	end;
}

2@cata,80,144,0	script	ins_2f_enter	-1,3,3,{
OnTouch_:
	donpcevent instance_npcname("ins_2f_enter_broad")+"::OnEnable";
	disablenpc instance_npcname("ins_2f_enter");
	end;
}

2@cata,1,1,0	script	ins_2f_enter_broad	-1,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("ins_2f_enter_broad");
	end;

OnEnable:
	enablenpc instance_npcname("ins_2f_enter_broad");
	initnpctimer;
	end;

OnTimer10000:
	mapannounce instance_mapname("2@cata"), "Baphomet : Humans... interfering again...",bc_map,"0xdb7093";
	end;

OnTimer13000:
	mapannounce instance_mapname("2@cata"), "바포메트의 사도 : Humans! Humans have invaded our sanctum!",bc_map,"0xFFFF00";
	end;

OnTimer16000:
	mapannounce instance_mapname("2@cata"), "바포메트의 사도 : Kill the humans! Do not stop the revival of our Master!",bc_map,"0xFFFF00";
	end;

OnTimer18000:
	mapannounce instance_mapname("2@cata"), "바포메트의 사도 : Hurry up and release the seals of the altars! Our Master's return is upon us!",bc_map,"0xFFFF00";
	stopnpctimer;
	disablenpc instance_npcname("ins_2f_enter_broad");
	end;
}

2@cata,50,67,0	script	slave_left	-1,5,5,{
OnTouch_:
	set .@map$, instance_mapname("2@cata");
	mapannounce .@map$, "바포메트의 사도 : Kill the humans! Don't let them interrupt the revival of our Master!",bc_map,"0xFFFF00";
	monster .@map$,55,67,"바포메트의 사도",1869,1;
	monster .@map$,51,67,"바포메트의 사도",1291,1;
	monster .@map$,58,67,"바포메트의 사도",1292,1;
	monster .@map$,53,67,"바포메트의 사도",1291,1;
	monster .@map$,54,67,"바포메트의 사도",1869,1;
	monster .@map$,55,67,"바포메트의 사도",1291,1;
	monster .@map$,56,67,"바포메트의 사도",1117,1;
	monster .@map$,58,66,"바포메트의 사도",1869,1;
	monster .@map$,56,66,"바포메트의 사도",1117,1;
	monster .@map$,60,66,"바포메트의 사도",1132,1;
	monster .@map$,59,66,"바포메트의 사도",1117,1;
	monster .@map$,54,66,"바포메트의 사도",1132,1;
	monster .@map$,55,66,"바포메트의 사도",1292,1;
	monster .@map$,56,66,"바포메트의 사도",1132,1;
	monster .@map$,50,65,"바포메트의 사도",1867,1;
	monster .@map$,61,65,"바포메트의 사도",1292,1;
	disablenpc instance_npcname("slave_left");
	end;
}

2@cata,109,67,0	script	slave_right	-1,5,5,{
OnTouch_:
	set .@map$, instance_mapname("2@cata");
	mapannounce .@map$, "바포메트의 사도 : Kill the humans! Don't let them interrupt the revival of our Master!",bc_map,"0xFFFF00";
	monster .@map$,105,67,"바포메트의 사도",1869,1;
	monster .@map$,104,67,"바포메트의 사도",1291,1;
	monster .@map$,107,67,"바포메트의 사도",1869,1;
	monster .@map$,106,67,"바포메트의 사도",1291,1;
	monster .@map$,102,67,"바포메트의 사도",1869,1;
	monster .@map$,103,67,"바포메트의 사도",1291,1;
	monster .@map$,103,67,"바포메트의 사도",1117,1;
	monster .@map$,109,66,"바포메트의 사도",1117,1;
	monster .@map$,108,66,"바포메트의 사도",1132,1;
	monster .@map$,101,66,"바포메트의 사도",1117,1;
	monster .@map$,106,66,"바포메트의 사도",1292,1;
	monster .@map$,102,66,"바포메트의 사도",1132,1;
	monster .@map$,104,66,"바포메트의 사도",1292,1;
	monster .@map$,103,66,"바포메트의 사도",1132,1;
	monster .@map$,109,65,"바포메트의 사도",1867,1;
	monster .@map$,108,65,"바포메트의 사도",1292,1;
	disablenpc instance_npcname("slave_right");
	end;
}

2@cata,79,39,0	script	slave_down	-1,5,5,{
OnTouch_:
	set .@map$, instance_mapname("2@cata");
	mapannounce .@map$, "바포메트의 사도 : Kill the humans! Don't let them interrupt the revival of our Master!",bc_map,"0xFFFF00";
	monster .@map$,78,41,"바포메트의 사도",1869,1;
	monster .@map$,79,42,"바포메트의 사도",1291,1;
	monster .@map$,78,46,"바포메트의 사도",1869,1;
	monster .@map$,81,41,"바포메트의 사도",1291,1;
	monster .@map$,81,42,"바포메트의 사도",1869,1;
	monster .@map$,79,43,"바포메트의 사도",1291,1;
	monster .@map$,77,40,"바포메트의 사도",1117,1;
	monster .@map$,79,41,"바포메트의 사도",1132,1;
	monster .@map$,79,42,"바포메트의 사도",1117,1;
	monster .@map$,79,43,"바포메트의 사도",1132,1;
	monster .@map$,79,48,"바포메트의 사도",1117,1;
	monster .@map$,78,49,"바포메트의 사도",1132,1;
	monster .@map$,78,41,"바포메트의 사도",1292,1;
	monster .@map$,74,42,"바포메트의 사도",1292,1;
	monster .@map$,72,48,"바포메트의 사도",1867,1;
	monster .@map$,72,38,"바포메트의 사도",1292,1;
	disablenpc instance_npcname("slave_down");
	end;
}

// Magical Seals
//============================================================
-	script	Magical Seal#SS	-1,{
	set .@seal_check,checkquest(3041,PLAYTIME);
	if (.@seal_check == 0 || .@seal_check == 1) {
		specialeffect2 EF_SILENCEATTACK;
		percentheal -50,0;
		sc_start Eff_Stone,30000,0;
		mes "SP가 아직 회복되지 않았습니다. 당신은 제단에서 SP를 잃었지만 인장의 힘이 돌아왔습니다.";
		close;
	}
	if (.@seal_check == 2) erasequest 3041;
	specialeffect EF_LEXDIVINA;
	disablenpc instance_npcname(strnpcinfo(0));
	set .@map$, instance_mapname("2@cata");
	if (strnpcinfo(2) == "0") areamobuseskill .@map$,79,81,10,1929,"NPC_INVINCIBLEOFF",1,0,0,ET_HELP,0;
	else if (strnpcinfo(2) == "2") areamobuseskill .@map$,123,109,10,1929,"NPC_INVINCIBLEOFF",1,0,0,ET_HELP,0;
	else if (strnpcinfo(2) == "4") areamobuseskill .@map$,123,22,10,1929,"NPC_INVINCIBLEOFF",1,0,0,ET_HELP,0;
	else if (strnpcinfo(2) == "8") areamobuseskill .@map$,35,21,10,1929,"NPC_INVINCIBLEOFF",1,0,0,ET_HELP,0;
	else if (strnpcinfo(2) == "10") areamobuseskill .@map$,35,109,10,1929,"NPC_INVINCIBLEOFF",1,0,0,ET_HELP,0;
	percentheal -50,0;
	sc_start Eff_Stone,20000,0;
	setquest 3041;
	mapannounce .@map$, "제단에 마법을 가해 인장이 활성화됩니다.",bc_map,"0x87ceeb";
	mes "제단에 힘이 돌아온 것이 느껴진다.";
	next;
	mes "하지만 제단에서 SP를 사용했기 때문에 3분동안 마법을 사용할 수 없다.";
	close;

OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
}
2@cata,79,81,0	duplicate(Magical Seal#SS)	Magical Seal#0	844
2@cata,123,109,0	duplicate(Magical Seal#SS)	Magical Seal#2	844
2@cata,123,22,0	duplicate(Magical Seal#SS)	Magical Seal#4	844
2@cata,35,21,0	duplicate(Magical Seal#SS)	Magical Seal#8	844
2@cata,35,109,0	duplicate(Magical Seal#SS)	Magical Seal#10	844

2@cata,79,65,0	script	The Main Altar#ss	844,{
	set .@party_id,getcharid(1);
	if (('ins_baphomet == 5) && (getpartyleader(.@party_id,2) == getcharid(0))) {
		mes "An evil power, too terrible to describe, lies under the great altar radiating a violet color.";
		next;
		mes "Complicated Magical Rune letters blink rapidly, attempting to suppress the dreadful power within.";
		next;
		mes "The bottom of the Main Altar trembles furiously.";
		next;
		specialeffect EF_METEORSTORM;
		specialeffect EF_METEORSTORM;
		mes "[" + strcharinfo(0) + "]";
		mes "Watch out! Something... Something is coming.";
		set 'ins_baphomet,6;
		donpcevent instance_npcname("ins_2f_hero_broad")+"::OnEnable";
		disablenpc instance_npcname("The Main Altar#ss");
		close;
	}
	else {
		mes "An evil power, too terrible to describe, lies under the great altar radiating a violet color.";
		next;
		mes "Complicated Magical Rune letters blink rapidly, attempting to suppress the dreadful power within.";
		close;
	}
	end;
}

2@cata,80,63,4	script	Ancient Hero's Soul#2F	411,{
	cutin "ins_cata_champ_s",2;
	if (checkquest(3041)>=0) erasequest 3041;
	mes "[Ancient Hero's Soul]";
	mes "Good job, my descendants... You've finished the long-cherished task that me and my bretheren could not complete.";
	next;
	mes "[Ancient Hero's Soul]";
	mes "I really appreciate your help.";
	mes "Our souls can finally rest in peace...";
	next;
	mes "[Ancient Hero's Soul]";
	mes "The struggle for peace on this world will never end. But... my role here is finally over because there are brave heroes like you.";
	next;
	switch(select("Let me leave the shrine.:Stop talking.")) {
	case 1:
		mes "[Ancient Hero's Soul]";
		mes "Okay. I'll let you and your group leave here safely.";
		next;
		mes "[Ancient Hero's Soul]";
		mes "If you leave, please say hello to Patrick for me.";
		cutin "",255;
		set 'ins_baphomet,0;
		warp "monk_test",310,150;
		close;
	case 2:
		mes "[Ancient Hero's Soul]";
		mes "Do you still have something to do here? If you're done I'll let you leave safely...";
		cutin "",255;
		close;
	}

OnInstanceInit:
	disablenpc instance_npcname("Ancient Hero's Soul#2F");
	end;
}

2@cata,1,2,0	script	ins_2f_hero_broad	-1,{
OnEnable:
	enablenpc instance_npcname("ins_2f_hero_broad");
	initnpctimer;
	end;

OnDisable:
	disablenpc instance_npcname("ins_2f_hero_broad");
	end;

OnTimer3000:
	mapannounce instance_mapname("2@cata"), "Ancient Hero's Soul : My God! The seal of the Main Altar is weakening!",bc_map,"0xFFFF00";
	end;

OnTimer6000:
	mapannounce instance_mapname("2@cata"), "Ancient Hero's Soul : My descendants... Listen carefully to what I'm going to say.",bc_map,"0xFFFF00";
	end;

OnTimer9000:
	mapannounce instance_mapname("2@cata"), "Ancient Hero's Soul : The altars that control the Main Altar's power are located in the Northeast, Southeast, Southwest and Northwest corners of this room.",bc_map,"0xFFFF00";
	end;

OnTimer12000:
	mapannounce instance_mapname("2@cata"), "Ancient Hero's Soul : Find these altars and activate their seals before Baphomet revives.",bc_map,"0xFFFF00";
	end;

OnTimer15000:
	mapannounce instance_mapname("2@cata"), "Baphomet : It's too late, weaklings... Now, you'll feel the despair of death!",bc_map,"0xdb7093";
	end;

OnTimer17000:
	mapannounce instance_mapname("2@cata"), "Baphomet : No one can harm me here. You will be my first sacrifice.",bc_map,"0xdb7093";
	donpcevent instance_npcname("control_baphomet")+"::OnEnable";
	donpcevent instance_npcname("ins_2f_hero_broad2")+"::OnEnable";
	stopnpctimer;
	disablenpc instance_npcname("ins_2f_hero_broad");
	end;
}

2@cata,3,3,0	script	control_baphomet	-1,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("control_baphomet");
	end;

OnEnable:
	enablenpc instance_npcname("control_baphomet");
	donpcevent instance_npcname("2f_callmon_pattern_c")+"::OnEnable";
	monster instance_mapname("2@cata"),79,64,"Baphomet#",1929,1,instance_npcname("control_baphomet")+"::OnMyMobDead";
	end;

OnMyMobDead:
	set .@map$, instance_mapname("2@cata");
	if (mobcount(.@map$,instance_npcname("control_baphomet")+"::OnMyMobDead") < 1) {
		set 'ins_baphomet,7;
		if (isbegin_quest(3041))
			erasequest 3041;
		mapannounce .@map$, "Baphomet : No! Nonono! How dare these weaklings defeat me!... No!!...",bc_map,"0xdb7093";
		enablenpc instance_npcname("Ancient Hero's Soul#2F");
		disablenpc instance_npcname("slave_down");
		disablenpc instance_npcname("slave_left");
		disablenpc instance_npcname("slave_right");
		donpcevent instance_npcname("ins_2f_hero_broad")+"::OnDisable";
		donpcevent instance_npcname("ins_2f_hero_broad2")+"::OnDisable";
		donpcevent instance_npcname("ins_2f_hero_pattern_c")+"::OnDisable";
		donpcevent instance_npcname("2f_callmon_pattern_c")+"::OnDisable";
		donpcevent instance_npcname("2f_callmon_pattern")+"::OnDisable";
		donpcevent instance_npcname("ins_2f_hero_pattern")+"::OnDisable";
		donpcevent instance_npcname("ins_2f_enter_broad")+"::OnDisable";
		donpcevent instance_npcname("control_baphomet")+"::OnDisable";
		disablenpc instance_npcname("control_baphomet");
	}
	end;
}

2@cata,2,2,0	script	ins_2f_hero_broad2	-1,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("ins_2f_hero_broad2");
	end;

OnEnable:
	enablenpc instance_npcname("ins_2f_hero_broad2");
	initnpctimer;
	end;

OnTimer8000:
	mapannounce instance_mapname("2@cata"), "Ancient Hero's Soul : Don't be discouraged, Baphomet can still be defeated!",bc_map,"0xFFFF00";
	end;

OnTimer11000:
	mapannounce instance_mapname("2@cata"), "Ancient Hero's Soul : Go to the altars and activate their seals.",bc_map,"0xFFFF00";
	end;

OnTimer13000:
	mapannounce instance_mapname("2@cata"), "Ancient Hero's Soul : Once the seals recover their power, Baphomet will be vulnerable.",bc_map,"0xFFFF00";
	end;

OnTimer16000:
	mapannounce instance_mapname("2@cata"), "Ancient Hero's Soul : You should lure Baphomet to the unsealed Altars. Otherwise, your efforts will be futile.",bc_map,"0xFFFF00";
	end;

OnTimer19000:
	mapannounce instance_mapname("2@cata"), "Ancient Hero's Soul : We have only 1 hour to stop Baphomet. If time runs out, the power of the seals will be useless.",bc_map,"0xFFFF00";
	end;

OnTimer22000:
	mapannounce instance_mapname("2@cata"), "Baphomet : It's useless. Make more seals. I'll crush them all. None of you will survive!",bc_map,"0xdb7093";
	end;

OnTimer26000:
	mapannounce instance_mapname("2@cata"), "Ancient Hero's Soul : The magical power of the central seal is running out. Go to the central seal and put the magical power.",bc_map,"0xFFFF00";
	enablenpc instance_npcname("Magical Seal#0");
	disablenpc instance_npcname("Magical Seal#2");
	disablenpc instance_npcname("Magical Seal#4");
	disablenpc instance_npcname("Magical Seal#8");
	disablenpc instance_npcname("Magical Seal#10");
	donpcevent instance_npcname("ins_2f_hero_pattern_c")+"::OnEnable";
	stopnpctimer;
	disablenpc instance_npcname("ins_2f_hero_broad2");
	end;
}

2@cata,4,1,0	script	2f_callmon_pattern_c	-1,{
OnInstanceInit:
	disablenpc instance_npcname("2f_callmon_pattern_c");
	end;

OnEnable:
	enablenpc instance_npcname("2f_callmon_pattern_c");
	initnpctimer;
	donpcevent instance_npcname("2f_callmon_pattern_c")+"::Ongo";
	end;

OnDisable:
	stopnpctimer;
	disablenpc instance_npcname("2f_callmon_pattern_c");
	end;

Ongo:
	donpcevent instance_npcname("2f_callmon_pattern")+"::OnEnable";
	end;

OnTimer3600000:
	donpcevent instance_npcname("2f_callmon_pattern_c")+"::OnDisable";
	end;
}

2@cata,4,2,0	script	2f_callmon_pattern	-1,{
OnInstanceInit:
	disablenpc instance_npcname("2f_callmon_pattern");
	end;

OnEnable:
	enablenpc instance_npcname("2f_callmon_pattern");
	stopnpctimer;
	initnpctimer;
	end;

OnDisable:
	disablenpc instance_npcname("2f_callmon_pattern");
	stopnpctimer;
	end;

OnTimer300000:
	enablenpc instance_npcname("slave_down");
	enablenpc instance_npcname("slave_left");
	enablenpc instance_npcname("slave_right");
	donpcevent instance_npcname("2f_callmon_pattern_c")+"::Ongo";
	end;
}

2@cata,3,1,0	script	ins_2f_hero_pattern_c	-1,{
OnInstanceInit:
	disablenpc instance_npcname("ins_2f_hero_pattern_c");
	end;

OnEnable:
	enablenpc instance_npcname("ins_2f_hero_pattern_c");
	initnpctimer;
	donpcevent instance_npcname("ins_2f_hero_pattern_c")+"::Ongo";
	end;

Ongo:
	donpcevent instance_npcname("ins_2f_hero_pattern")+"::OnEnable";
	end;

OnDisable:
	stopnpctimer;
	disablenpc instance_npcname("Magical Seal#0");
	disablenpc instance_npcname("Magical Seal#2");
	disablenpc instance_npcname("Magical Seal#4");
	disablenpc instance_npcname("Magical Seal#8");
	disablenpc instance_npcname("Magical Seal#10");
	donpcevent instance_npcname("ins_2f_hero_pattern")+"::OnDisable";
	disablenpc instance_npcname("ins_2f_hero_pattern_c");
	end;

OnTimer3600000:
	mapannounce instance_mapname("2@cata"), "Baphomet : krrrr... Now you can't stop me with the seals. All you can do is wait for death!",bc_map,"0xdb7093";
	end;

OnTimer3605000:
	mapannounce instance_mapname("2@cata"), "Ancient Hero's Soul : We can't stop Baphomet with the magical power of the seals anymore. Now everything depends on God...",bc_map,"0xFFFF00";
	donpcevent instance_npcname("ins_2f_hero_pattern_c")+"::OnDisable";
	end;
}

2@cata,3,2,0	script	ins_2f_hero_pattern	-1,{
OnInstanceInit:
	disablenpc instance_npcname("ins_2f_hero_pattern");
	end;

OnEnable:
	enablenpc instance_npcname("ins_2f_hero_pattern");
	initnpctimer;
	end;

OnDisable:
	disablenpc instance_npcname("ins_2f_hero_pattern");
	stopnpctimer;
	end;

OnTimer70000:
	switch(rand(1,5)) {
	case 1:
		mapannounce instance_mapname("2@cata"), "Ancient Hero's Soul : The seal of the Main Altar is running out. Strengthen the Main Altar's seal!",bc_map,"0xFFFF00";
		enablenpc instance_npcname("Magical Seal#0");
		disablenpc instance_npcname("Magical Seal#2");
		disablenpc instance_npcname("Magical Seal#4");
		disablenpc instance_npcname("Magical Seal#8");
		disablenpc instance_npcname("Magical Seal#10");
		break;
	case 2:
		mapannounce instance_mapname("2@cata"), "Ancient Hero's Soul : The magical power of the seal at 2 o'clock is running out. Go to 2 o'clock and put the magical power in the seal.",bc_map,"0xFFFF00";
		disablenpc instance_npcname("Magical Seal#0");
		enablenpc instance_npcname("Magical Seal#2");
		disablenpc instance_npcname("Magical Seal#4");
		disablenpc instance_npcname("Magical Seal#8");
		disablenpc instance_npcname("Magical Seal#10");
		break;
	case 3:
		mapannounce instance_mapname("2@cata"), "Ancient Hero's Soul : The magical power of the seal at 4 o'clock is running out. Go to 4 o'clock and put the magical power in the seal.",bc_map,"0xFFFF00";
		disablenpc instance_npcname("Magical Seal#0");
		disablenpc instance_npcname("Magical Seal#2");
		enablenpc instance_npcname("Magical Seal#4");
		disablenpc instance_npcname("Magical Seal#8");
		disablenpc instance_npcname("Magical Seal#10");
		break;
	case 4:
		mapannounce instance_mapname("2@cata"), "Ancient Hero's Soul : The magical power of the seal at 8 o'clock is running out. Go to 8 o'clock and put the magical power in the seal.",bc_map,"0xFFFF00";
		disablenpc instance_npcname("Magical Seal#0");
		disablenpc instance_npcname("Magical Seal#2");
		disablenpc instance_npcname("Magical Seal#4");
		enablenpc instance_npcname("Magical Seal#8");
		disablenpc instance_npcname("Magical Seal#10");
		break;
	case 5:
		mapannounce instance_mapname("2@cata"), "Ancient Hero's Soul : The magical power of the seal at 10 o'clock is running out. Go to 10 o'clock and put the magical power in the seal.",bc_map,"0xFFFF00";
		disablenpc instance_npcname("Magical Seal#0");
		disablenpc instance_npcname("Magical Seal#2");
		disablenpc instance_npcname("Magical Seal#4");
		disablenpc instance_npcname("Magical Seal#8");
		enablenpc instance_npcname("Magical Seal#10");
	}
	stopnpctimer;
	donpcevent instance_npcname("ins_2f_hero_pattern_c")+"::Ongo";
	end;
}


1@cata,1,1,0	script	ins_1f_spawn_mobs	-1,{
OnInstanceInit:
	set .@map$, instance_mapname("1@cata");
	monster .@map$,0,0,"Zombie Master",1298,10;
	monster .@map$,0,0,"Wraith Dead",1291,10;
	//monster .@map$,0,0,"Ancient Mimic",1699,10;
	monster .@map$,0,0,"Flame Skull",1869,10;
	monster .@map$,0,0,"Skeleton General",1290,10;
	monster .@map$,0,0,"Zombie Master",1298,10;
	monster .@map$,0,0,"Skeleton General",1290,10;
	monster .@map$,0,0,"Flame Skull",1869,10;
	monster .@map$,0,0,"Wraith Dead",1291,10;
	monster .@map$,0,0,"Wraith Dead",1291,10;
	monster .@map$,0,0,"Khalitzburg",1132,10;
	monster .@map$,0,0,"Khalitzburg",1132,10;
	monster .@map$,0,0,"Flame Skull",1869,10;
	monster .@map$,0,0,"Flame Skull",1869,10;
	monster .@map$,0,0,"Ancient Mimic",1699,10;
	monster .@map$,0,0,"Zombie Master",1298,10;
	monster .@map$,0,0,"Ancient Mimic",1699,10;
	monster .@map$,0,0,"Zombie Master",1298,10;
	monster .@map$,0,0,"Wraith Dead",1291,10;
	monster .@map$,0,0,"Skeleton General",1290,10;
	monster .@map$,0,0,"Skeleton General",1290,10;
	monster .@map$,0,0,"Wind Ghost",1263,10;
	monster .@map$,0,0,"Wind Ghost",1263,10;
	monster .@map$,0,0,"Wind Ghost",1263,10;
	monster .@map$,0,0,"Lude",1509,10;
	monster .@map$,0,0,"Lude",1509,10;
	monster .@map$,0,0,"Evil Druid",1117,10;
	monster .@map$,0,0,"Evil Druid",1117,10;
	monster .@map$,0,0,"Evil Druid",1117,10;
	monster .@map$,0,0,"Banshee",1867,10;
	monster .@map$,0,0,"Dark Illusion",1302,1;
	disablenpc instance_npcname("ins_1f_spawn_mobs");
	end;
}
