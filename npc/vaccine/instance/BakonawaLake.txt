//===== rAthena Script ======================================= 
//= Bakonawa Lake
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Defeat Bakonawa before he eats the moon.
//= Part of the "Bakonawa Extermination" quest.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Euphy]
//= 1.1 Added GM management NPC. [Euphy]
//= 1.2 Added VIP features. [Euphy]
//============================================================ 

// Instance Creation :: ma_baku
//============================================================
ma_scene01,174,179,4	script	따호	541,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "너는 너무 많은 물건을 나르고있다. 그들 중 일부를 벗은 후에 다시 오십시오.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "당신은 너무 많은 체중을 나르고 있습니다. 무게를 줄인 후에 다시 오십시오.";
		close;
	}
	if (BaseLevel < 140) {
		mes "[따호]";
		mes "이봐요. 여기는 당신에게 너무 위험한 장소입니다. 자신을 안전하게 지킬 수는 없습니다. 마을로 돌아가세요!";
		close;
	}
//	if (malaya_bakona1 < 7 && malaya_bakona2 < 15) {
//		mes "[따호]";
//		mes "우리는 그 바코 나와에 의해 모두 죽을거야. 아, 우리는 그를 죽여야 만하지만 불행히도 우리는 준비가되어 있지 않습니다.";
//		close;
//	}

	set .@party_id,getcharid(1);
	set .@md_name$, "Bakonawa Lake";

	set .@baku_time, checkquest(12278,PLAYTIME);
	if (.@baku_time == -1) {
		if (checkquest(12279) == -1) setquest 12279;
	}
	if (!.@party_id) {
		mes "[따호]";
		mes "당신과 함께 할 수있는 파티가 없습니까? 아니면 혼자서라도 파티를 만드십시오.";
		close;
	}
	if (.@baku_time == -1) {
		if (getcharid(0) == getpartyleader(.@party_id,2)) {
			mes "[따호]";
			mes "당신은 버코너워 사냥꾼들의 리더입니까? 길은 잘 못되어 로프로 절벽을 내려야 합니다. 괜찮나요?";
			next;
			set .@i, select("밧줄을 엮으십시오.:이제 내가 내려갈거야.: 취소.");
		} else {
			mes "[따호]";
			mes "당신은 바코나와를 살해하기 위해 왔습니다. 당신의 파티 리더는 제가 밧줄을 짜는 것을 도와야합니다.";
			next;
			set .@i, select(":이제 나는 내려갈 것이다.:취소.");
		}
		switch(.@i) {
		case 1:
			if (instance_create(.@md_name$) < 0) {
				mes "[따호]";
				mes "오, 밧줄이 떨어졌습니다. 나는 새 것을 만들어야 해.";
				close;
			}
			mes "[따호]";
			mes "지금은 짜는 중입니다. 그래서 끝나면 내려갈 수 있습니다.";
			close;
		case 2:
			switch(instance_enter(.@md_name$)) {
			case IE_OTHER:
				mes "[따호]";
				mes "알 수없는 오류가 발생했습니다.";
				close;
			case IE_NOINSTANCE:
				mes "[따호]";
				mes "로프는 아직 짜여지지 않았습니다. 조금만 기다려주세요.";
				close;
			case IE_NOMEMBER:
				mes "[따호]";
				mes "민간인은 입장 할 수 없습니다.";
				close;
			case IE_OK:
				mapannounce "ma_scene01","A party member, "+strcharinfo(0)+" of the party "+getpartyname(.@party_id)+" is entering the dungeon, "+.@md_name$+".",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
				setquest 12278;
				//warp "1@ma_b",64,51;
				close;
			}
		case 3:
			close;
		}
	} else if (.@baku_time == 0 || .@baku_time == 1) {
		mes "[따호]";
		mes "호수에 도착하기 위해 사용 된 로프는 이미 부러졌습니다. 우리는 지금 주변을 청소하고 있습니다. 잠시 후 돌아 오십시오.";
		close;
	} else if (.@baku_time == 2) {
		mes "[따호]";
		mes "주변은 모두 청소했습니다. 이제 로프를 얻은 후에 다시 내려 갈 수 있습니다.";
		erasequest 12278;
		close;
	}
}

sec_in02,26,26,4	script	Bakonawa's Rage	541,{
	callfunc "F_GM_NPC";
	mes "[따호]";
	mes "My name is Minky.";
	if (callfunc("F_GM_NPC",1854,0) == 1) {
		erasequest 12278;
		erasequest 12279;
		set malaya_bakona1,7;
		set malaya_bakona2,15;
	}
	close;
}

// Instance Scripts :: in_baku01
//============================================================
1@ma_b,62,52,4	script	따호#nf	541,{
	mes "[따호]";
	mes "헤헤, 저도 버코너워의 전투에 직접 참여하고 싶습니다만, 그냥 용감하게 저 뒤 바위에 숨어서 여러분들을 응원하겠습니다.";
	next;
	mes "[따호]";
	mes "버코너워는 아무런 공격이 통하지 않는 상태가 되기도 하는데 그땐 제가 시키는데로 움직이세요. 저 따호만 믿으시면 모든게 술술 풀릴겁니다.";
	next;
	if (getcharid(0) == getpartyleader(getcharid(1),2)) {
		mes "[따호]";
		mes "이제 제가 버코너워를 물 위로 나오게 할 것이다.";
		next;
		switch(select("잠깐! 아직이다!:좋아 녀석을 잡아보자!")) {
		case 1:
			mes "[따호]";
			mes "오 ... 그래 ... 준비 됐으면 말해줘.";
			close;
		case 2:
			mes "[따호]";
			mes "그럼 이 호수에 알보팔이라는 약물을 한방울 떨어뜨려 보겠습니다.";
			next;
			mes "[따호]";
			mes "버코너워 녀석이 급격한 상황변화를 받아들이지 못하고 폭력적으로 변할 것이 눈에 선 하군요 ...";
			donpcevent instance_npcname("#Bakonawan1")+"::OnStart";
			disablenpc instance_npcname("따호#nf");
			close;
		}
	} else {
		mes "[따호]";
		mes "제가 알보팔을 떨어뜨려 버코너워를 화나게 했어요. 우리가 그를 잡으면 버코너워는 좋은 단백질이 될거에요.";
		close;
	}
}

1@ma_b,36,111,4	script	#Bakonawan1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan1");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan1");
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"따호: I can see him there on the surface! ATTACK!!!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	monster .@map$,78,81,"Bakonawa",2320,1,instance_npcname("#Bakonawan1")+"::OnMyMobDead";
	initnpctimer;
	end;
OnFail:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan1")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@ma_b");
	if (mobcount(.@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("#BakonawaDeadn1")+"::OnStart";
		killmonster .@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan1");
	}
	end;
OnTimer1000:	callsub OnAnnounce,"제한시간 10분";
OnTimer60000:	callsub OnAnnounce,"제한시간 9분";
OnTimer120000:	callsub OnAnnounce,"제한시간 8분";
OnTimer180000:	callsub OnAnnounce,"제한시간 7분";
OnTimer240000:	callsub OnAnnounce,"제한시간 6분";
OnTimer300000:	callsub OnAnnounce,"제한시간 5분";
OnTimer360000:	callsub OnAnnounce,"제한시간 4분";
OnTimer420000:	callsub OnAnnounce,"제한시간 3분";
OnTimer480000:	callsub OnAnnounce,"제한시간 2분";
OnTimer540000:	callsub OnAnnounce,"제한시간 1분";
OnTimer570000:
	mapannounce instance_mapname("1@ma_b"),"시간 제한: 30 초 전",bc_map,"0xff4400"; //FW_NORMAL 18 0 0
	donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
OnTimer600000:
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"버코너워가 호수에서 멀리 빠져나갔습니다.",bc_map,"0xffff00"; //FW_NORMAL 20 0 0
	donpcevent instance_npcname("따호#Fail")+"::OnStart";
	donpcevent instance_npcname("#Bakonawan1")+"::OnFail";
	killmonster .@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan1");
	end;
OnAnnounce:
	mapannounce instance_mapname("1@ma_b"),"시간 제한: "+getarg(0),bc_map,"0xff4400"; //FW_NORMAL 15 0 0
	donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
}

1@ma_b,78,81,0	script	#SummonPuppetsn1	139,4,5,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#SummonPuppetsn1");
	end;
OnStart:
	stopnpctimer;
	enablenpc instance_npcname("#SummonPuppetsn1");
	initnpctimer;
	end;
OnTouch:
	stopnpctimer;
	disablenpc instance_npcname("#SummonPuppetsn1");
	end;
OnTimer5000:
	setarray .@x[0],79,71,60,61,57,89,95,96,99;
	setarray .@y[0],71,72,80,90,99,73,82,90,99;
	set .@map$, instance_mapname("1@ma_b");
	for(set .@i,0; .@i<9; set .@i,.@i+1) {
		set .@rand, rand(1,10);
		if (.@rand > 7)
			monster .@map$,.@x[.@i],.@y[.@i],"버코너워의 의지",2337,1,instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
		else if (.@rand < 4)
			monster .@map$,.@x[.@i],.@y[.@i],"버코너워의 의지",2343,1,instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
	}
	end;
OnTimer50000:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#SummonPuppetsn1");
	end;
OnMyMobDead:
	end;
}

1@ma_b,1,5,4	script	#BakonawaDeadn1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#BakonawaDeadn1");
	end;
OnStart:
	enablenpc instance_npcname("#BakonawaDeadn1");
	initnpctimer;
	end;
OnTimer100:
	mapannounce instance_mapname("1@ma_b"),"따호: 좋아요. 버코너워가 물 아래로 숨었습니다! 이제 녀석은 달을 삼키려고 할거에요.",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer5000:
	mapannounce instance_mapname("1@ma_b"),"따호: 절대 녀석이 달에 집중하지 못하도록 솥과 징을 쳐서 시끄럽게 만들어야해요!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_b"),"따호: 녀석이 물 위로 나타나더라도 절대 녀석을 공격하지 마세요. 솥과 징에만 신경쓰세요!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer15000:
	mapannounce instance_mapname("1@ma_b"),"목표 - 호수 좌 우의 솥과 징 4개를 파괴할 것",bc_map,"0xff3300"; //FW_NORMAL 15 0 0
	donpcevent instance_npcname("#Bakonawan2")+"::OnStart";
	stopnpctimer;
	disablenpc instance_npcname("#BakonawaDeadn1");
	end;
}

1@ma_b,36,111,4	script	#Bakonawan2	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan2");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan2");
	set .@label$, instance_npcname("#Bakonawan2")+"::OnMyMobDead";
	set .@map$, instance_mapname("1@ma_b");
	monster .@map$,95,98,"가마솥",2328,1,.@label$;
	monster .@map$,60,98,"가마솥",2328,1,.@label$;
	monster .@map$,97,104,"징",2328,1,.@label$;
	monster .@map$,58,104,"징",2328,1,.@label$;
	donpcevent instance_npcname("#Bakonawan2-1")+"::OnStart";
	initnpctimer;
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@ma_b");
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("#Bakonawan2")+"::OnMyMobDead");
	if (.@mob_dead_num < 1) {
		donpcevent instance_npcname("#Bakonawan2-1")+"::OnEnd";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan2");
	} else
		mapannounce .@map$,"따호: 좋아! 이제 "+.@mob_dead_num+" 개 남았습니다!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	 end;
OnTimer1000:	callsub OnAnnounce,"제한시간 5분",15,1;
OnTimer60000:	callsub OnAnnounce,"제한시간 4분",15,1;
OnTimer120000:	callsub OnAnnounce,"제한시간 3분",15,1;
OnTimer180000:	callsub OnAnnounce,"제한시간 2분",15,1;
OnTimer240000:	callsub OnAnnounce,"제한시간 1분",15,1;
OnTimer270000:	callsub OnAnnounce,"제한시간 30초",15,0;
OnTimer280000:	callsub OnAnnounce,"제한시간 20초",15,0;
OnTimer290000:	callsub OnAnnounce,"제한시간 10초",15,0;
OnTimer295000:	callsub OnAnnounce,"제한시간 5초",16,0;
OnTimer296000:	callsub OnAnnounce,"제한시간 4초",17,0;
OnTimer297000:	callsub OnAnnounce,"제한시간 3초",18,0;
OnTimer298000:	callsub OnAnnounce,"제한시간 1초",19,0;
OnTimer299000:	callsub OnAnnounce,"제한시간 1초",20,0;
OnTimer300000:
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"버코너워가 호수에서 멀리 빠져나갔습니다.",bc_map,"0xffff00"; //FW_NORMAL 20 0 0
	donpcevent instance_npcname("따호#Fail")+"::OnStart";
	donpcevent instance_npcname("#Bakonawan2-1")+"::OnFail";
	killmonster .@map$,instance_npcname("#Bakonawan2")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan2");
	end;
OnAnnounce:
	mapannounce instance_mapname("1@ma_b"),"제한 시간: "+getarg(0),bc_map,"0xff4400"; //FW_NORMAL getarg(1) 0 0
	if (getarg(2)) donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
}

1@ma_b,36,111,4	script	#Bakonawan2-1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan2-1");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan2-1");
	monster instance_mapname("1@ma_b"),78,93,"분노한 버코너워",2321,1,instance_npcname("#Bakonawan2-1")+"::OnMyMobDead";
	end;
OnFail:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan2-1")+"::OnMyMobDead";
	end;
OnEnd:
	enablenpc instance_npcname("#Bakonawan2-1");
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan2-1")+"::OnMyMobDead";
	initnpctimer;
	end;
OnTimer1000:
	mapannounce instance_mapname("1@ma_b"),"따호: 녀석이 물 속으로 다시 숨어버렸군요. 이제 끝난건가?",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer5000:
	mapannounce instance_mapname("1@ma_b"),"따호: 앗, 물 속에서 뭔가 빠르게 솟아 오르고 있어요! 녀석이... 녀석이 올라옵니다!!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_b"),"목표 - 더 흉포해진 버코너워를 처치",bc_map,"0xff3300"; //FW_NORMAL 15 0 0
	donpcevent instance_npcname("#Bakonawan3")+"::OnStart";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan2-1");
	end;
OnMyMobDead:
	end;
}

1@ma_b,36,111,4	script	#Bakonawan3	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan3");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan3");
	initnpctimer;
	monster instance_mapname("1@ma_b"),78,81,"분노한 버코너워",2322,1,instance_npcname("#Bakonawan3")+"::OnMyMobDead";
	donpcevent instance_npcname("#Bakonawan3-1")+"::OnStart";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@ma_b");
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead");
	if (.@mob_dead_num < 1) {
		mapannounce .@map$,"따호: 녀석을 해치웠군요! 녀석이 상자처럼 보이는 것을 토해낸 것이 보이는군요.",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
		donpcevent instance_npcname("따호#Completed")+"::OnStart";
		donpcevent instance_npcname("#Bakonawan3-1")+"::OnEnd";
		killmonster .@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan3");
	}
	end;
OnTimer1000:
	callsub OnAnnounce,"10 minutes",15,1;
	end;
OnTimer60000:
OnTimer120000:
OnTimer180000:
OnTimer240000:
	donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
OnTimer300000:	callsub OnAnnounce,"제한시간 5분",15,1;
OnTimer360000:	callsub OnAnnounce,"제한시간 4분",15,1;
OnTimer420000:	callsub OnAnnounce,"제한시간 3분",15,1;
OnTimer480000:	callsub OnAnnounce,"제한시간 2분",15,1;
OnTimer540000:	callsub OnAnnounce,"제한시간 1분",15,1;
OnTimer570000:	callsub OnAnnounce,"제한시간 30초",15,0;
OnTimer580000:	callsub OnAnnounce,"제한시간 20초",15,0;
OnTimer590000:	callsub OnAnnounce,"제한시간 10초",15,0;
OnTimer595000:	callsub OnAnnounce,"제한시간 5초",16,0;
OnTimer596000:	callsub OnAnnounce,"제한시간 4초",17,0;
OnTimer597000:	callsub OnAnnounce,"제한시간 3초",18,0;
OnTimer598000:	callsub OnAnnounce,"제한시간 2초",19,0;
OnTimer599000:	callsub OnAnnounce,"제한시간 1초",20,0;
OnTimer600000:
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"버코너워가 호수에서 멀리 빠져나갔습니다.",bc_map,"0xffff00"; //FW_NORMAL 20 0 0
	donpcevent instance_npcname("따호#Fail")+"::OnStart";
	killmonster .@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan3");
	end;
OnAnnounce:
	mapannounce instance_mapname("1@ma_b"),"제한 시간: "+getarg(0),bc_map,"0xff4400"; //FW_NORMAL getarg(1) 0 0
	if (getarg(2)) donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
}

1@ma_b,36,111,4	script	#Bakonawan3-1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan3-1");
	end;
OnStart:
	initnpctimer;
	end;
OnEnd:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
	//showdigit 0;
	stopnpctimer;
	end;
OnMyMobDead:
	showdigit mobcount(instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead");
	end;
OnTimer120000:	callsub OnMobSpawn,10;
OnTimer180000:	callsub OnMobSpawn,15;
OnTimer240000:	callsub OnMobSpawn,20;
//OnTimer300000:	callsub OnMobSpawn,25;
OnTimer300000:	callsub OnMobSpawn,30;
OnTimer360000:	callsub OnMobSpawn,35;
OnTimer420000:	callsub OnMobSpawn,40;
OnTimer480000:	callsub OnMobSpawn,45;
OnTimer540000:	callsub OnMobSpawn,50;
OnTimer600000:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
	stopnpctimer;
	end;
OnMobSpawn:
	set .@label$, instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
	set .@map$, instance_mapname("1@ma_b");
	killmonster .@map$,.@label$;
	set .@mob_dead_num, mobcount(.@map$,.@label$);
	if (.@mob_dead_num < 50) {
		set .@mob_rg, 50 - .@mob_dead_num;
		if (.@mob_rg > getarg(0))
			set .@mob_rg, getarg(0);
		while(1) {
			areamonster .@map$,74,74,82,74,"버코너워의 꼭두각시",2334,1,.@label$;
			set .@mob_dead_num, mobcount(.@map$,.@label$);
			if (.@mob_dead_num >= .@mob_rg) {
				break;
			}
		}
	}
	end;
}

1@ma_b,62,52,4	script	따호#Completed	541,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "너는 너무 많은 종류의 물건을 나르고있는 것 같아. 줄이면 다시 시도하십시오.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "이미 너무 많은 무게의 물건을 들고있는 것 같습니다. 무게를 줄인 후에 다시 시도하십시오.";
		close;
              }
	if (checkquest(12279,HUNTING) == 2) {
		erasequest 12279;
		mes "[따호]";
		mes "버코너워 시체에서 이게 나왔어요. 가져가시는게 좋겠어요.";
		if (!VIP_SCRIPT || vip_status(VIP_STATUS_ACTIVE))
			getitem 6499,7; //Ancient_Grudge
		else
			getitem 6499,5; //Ancient_Grudge
		getexp 0,100000;
		set #CASHPOINTS, #CASHPOINTS+2000;
		next;
	}
	mes "[따호]";
	mes "서둘러서이 장소에서 나가는 것이 좋습니다. 이 곳은 바코 나와가 없어도 여전히 위험합니다 ...";
	next;
	mes "[따호]";
	mes "그럼 로프를 타고 올라 가세요!";
	close2;
	warp "ma_scene01",175,176;
	end;
OnInstanceInit:
	disablenpc instance_npcname("따호#Completed");
	end;
OnStart:
	enablenpc instance_npcname("따호#Completed");
	initnpctimer;
	end;
OnTimer1000:
	monster instance_mapname("1@ma_b"),78,74,"버코너워 보물상자",2335,1;
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_b"),"따호: 따로 줄 것이 있으니 상자를 열고나서 언덕 위에 있는 저에게 오세요.",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	stopnpctimer;
	end;
}

1@ma_b,62,52,4	script	따호#Fail	541,{
	mes "[따호]";
	mes "흠. 우리는 거의 그를 잡았지만 그는 빠른 생물 중 하나입니다 ...";
	next;
	if (getcharid(0) == getpartyleader(getcharid(1),2)) {
		mes "[따호]";
		mes "다시 시도 할 건가요?";
		next;
		switch(select("잠깐만!:물론! 우리는 지금 뒤로 물러 설 수 없습니다!")) {
		case 1:
			mes "[따호]";
			mes "어... 녀석이 거의 다 왔어요.";
			close;
		case 2:
			mes "[따호]";
			mes "그 다음 나는 호수에 알보팔을 다시 한번 사용할 것입니다.";
			next;
			mes "[따호]";
			mes "우리는 격렬한 분노로 버코너워를 보려고합니다 ...";
			disablenpc instance_npcname("따호#Fail");
			donpcevent instance_npcname("#Bakonawan1")+"::OnStart";
			close;
		}
		close;
	} else {
		mes "[따호]";
		mes "이미 알보팔을 떨어뜨렸습니다. 버코너워가 화났으면 좋겠네요...";
		close;
	}

OnInstanceInit:
	disablenpc instance_npcname("따호#Fail");
	end;
OnStart:
	enablenpc instance_npcname("따호#Fail");
	end;
}
