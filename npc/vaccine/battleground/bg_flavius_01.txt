// ==============================================================================
// BattleGround System - Flavius 1
// ==============================================================================

// Registration NPC's
// *********************************************************************

bat_room,253,97,4	script	Registration::Fl1R_Guillaume	418,{
	end;
OnInit:
	waitingroom "전투 대기실 - "+$Flavi+" 명",$Flavi+1,"Flavius_BG1::OnGuillaumeJoin",1;
	end;
OnEnterBG:
	set $@FlaviusBG1_id1, waitingroom2bg("bat_b01",390,10,"Flavius_BG1::OnGuillaumeQuit","");
	end;
OnFlaNoin:
	delwaitingroom;
	waitingroom "전투 대기실 - 인원 조정중",0;
	end;
OnFlaWait:
	delwaitingroom;
	waitingroom "전투 대기실 - "+$Flavi+" 명",$Flavi+1,"Flavius_BG1::OnGuillaumeJoin",1;
	end;

}

bat_room,253,74,0	script	Registration::Fl1R_Croix	414,{
	end;
OnInit:
	waitingroom "전투 대기실 - "+$Flavi+" 명",$Flavi+1,"Flavius_BG1::OnCroixJoin",1;
	end;
OnEnterBG:
	set $@FlaviusBG1_id2, waitingroom2bg("bat_b01",10,290,"Flavius_BG1::OnCroixQuit","");
	end;
OnFlaNoin:
	delwaitingroom;
	waitingroom "전투 대기실 - 인원 조정중",0;
	end;
OnFlaWait:
	delwaitingroom;
	waitingroom "전투 대기실 - "+$Flavi+" 명",$Flavi+1,"Flavius_BG1::OnCroixJoin",1;
	end;
}

// Battleground Engine
// *********************************************************************

-	script	Flavius_BG1	-1,{
	end;

OnInit:
	disablenpc "기욤 이십인장#fl1";
	disablenpc "크루아 이십인장#fl1";
	disablenpc "전장의 치료사#fl11";
	disablenpc "전장의 치료사#fl12";
	end;

OnGuillaumeQuit:
OnCroixQuit:
	set BG_Delay_Tick, gettimetick(2) + 600;
	end;

OnGuillaumeJoin:
OnCroixJoin:
	if( $@FlaviusBG1 == 0 )
		donpcevent "Flavius_BG1::OnReadyCheck";
	end;

OnReadyCheck:
	if( $@FlaviusBG1 )
		end;
	set .@Guillaume, getwaitingroomstate(0,"Fl1R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"Fl1R_Croix");
	
	if( .@Guillaume < $Flavi || .@Croix < $Flavi || .@Guillaume != .@Croix )
	{
		mapannounce "bat_room","전장 - 플라비우스 전투 기욤 진영 : 현재 " + .@Guillaume + "/"+$Flavi+"명, 크루아 진영 : 현재 " + .@Croix + "/"+$Flavi+"명",1,0x006400;
		end;
	}

	sleep 10000;
	
	set .@Guillaume_R, getwaitingroomstate(0,"Fl1R_Guillaume"); 
	set .@Croix_R, getwaitingroomstate(0,"Fl1R_Croix"); 

	if( .@Guillaume_R != .@Croix_R ) 
	{ 
		donpcevent "Flavius_BG1::OnReadyCheck"; 
		end; 
	} 

	// BG Variables
	set $@FlaviusBG1, 1;
	set $@FlaviusBG1_Victory, 0;
	set .Guillaume_Score, 0;
	set .Guillaume_Loss, 0;
	set .Croix_Score, 0;
	set .Croix_Loss, 0;
	set .Match, 0;
	// Prepare NPC
	donpcevent "#gfl1_respawn::OnBGStart";
	donpcevent "#cfl1_respawn::OnBGStart";
	enablenpc "전장의 치료사#fl11";
	enablenpc "전장의 치료사#fl12";
	disablenpc "기욤 이십인장#fl1";
	disablenpc "크루아 이십인장#fl1";
	// Build and Warp Teams
	donpcevent "Fl1R_Guillaume::OnEnterBG";
	donpcevent "Fl1R_Croix::OnEnterBG";
	announce "전장 - 플라비우스 전투가 시작되었습니다!",0,0x006400;
	initnpctimer;
	// Start Match!!

OnRoundStart:
	sleep 2000;
	if( $@FlaviusBG1 != 1 ) end;

	areapercentheal "bat_b01",382,2,397,17,100,100;
	areapercentheal "bat_b01",2,282,17,297,100,100;
	bg_warp $@FlaviusBG1_id1,"bat_b01",311,224;
	bg_warp $@FlaviusBG1_id2,"bat_b01",87,75;

	sleep 2000;
	if( $@FlaviusBG1 != 1 ) end;

	set .Match, .Match + 1;
	// Crystal Spawn
	set .Guillaume_Crystal, bg_monster($@FlaviusBG1_id1,"bat_b01",328,150,"기욤 크리스탈",1914,"Flavius_BG1::OnGuillaumeBreak");
	setwall "bat_b01",327,149,2,6,0,"gui_wall1-1";
	setwall "bat_b01",329,149,2,0,0,"gui_wall1-2";
	setwall "bat_b01",329,151,2,2,0,"gui_wall1-3";
	setwall "bat_b01",327,151,2,4,0,"gui_wall1-4";

	set .Croix_Crystal, bg_monster($@FlaviusBG1_id2,"bat_b01",62,150,"크루아 크리스탈",1915,"Flavius_BG1::OnCroixBreak");
	setwall "bat_b01",61,149,2,6,0,"cro_wall1-1";
	setwall "bat_b01",63,149,2,0,0,"cro_wall1-2";
	setwall "bat_b01",63,151,2,2,0,"cro_wall1-3";
	setwall "bat_b01",61,151,2,4,0,"cro_wall1-4";

	// Guardian Spawns
	bg_monster $@FlaviusBG1_id1,"bat_b01",328,160,"기욤 가디언",1949,"Flavius_BG1::OnGuiGuardian";
	bg_monster $@FlaviusBG1_id1,"bat_b01",328,140,"기욤 가디언",1950,"Flavius_BG1::OnGuiGuardian";
	set .Guillaume_Guardian, 2;
	bg_monster $@FlaviusBG1_id2,"bat_b01",62,160,"크루아 가디언",1949,"Flavius_BG1::OnCroGuardian";
	bg_monster $@FlaviusBG1_id2,"bat_b01",61,140,"크루아 가디언",1950,"Flavius_BG1::OnCroGuardian";
	set .Croix_Guardian, 2;
	// Announces
	mapannounce "bat_b01","플라비우스 전장 - [ " + .Match + " ] 번째 라운드가 시작되었습니다!",1,0x006400;
	end;

OnRoundStop:
	// Remove Monsters
	killmonster "bat_b01","Flavius_BG1::OnGuiGuardian";
	killmonster "bat_b01","Flavius_BG1::OnCroGuardian";
	delwall "gui_wall1-1";
	delwall "gui_wall1-2";
	delwall "gui_wall1-3";
	delwall "gui_wall1-4";
	killmonster "bat_b01","Flavius_BG1::OnGuillaumeBreak";
	delwall "cro_wall1-1";
	delwall "cro_wall1-2";
	delwall "cro_wall1-3";
	delwall "cro_wall1-4";
	killmonster "bat_b01","Flavius_BG1::OnCroixBreak";
	end;

OnGuiGuardian:
	if( set(.Guillaume_Guardian, .Guillaume_Guardian - 1) <= 0 )
	{
		delwall "gui_wall1-1";
		delwall "gui_wall1-2";
		delwall "gui_wall1-3";
		delwall "gui_wall1-4";
		mapannounce "bat_b01","기욤 진영의 크리스탈 공격 가능!",1,0x0000FF;
	}
	end;

OnCroGuardian:
	if( set(.Croix_Guardian, .Croix_Guardian - 1) <= 0 )
	{
		delwall "cro_wall1-1";
		delwall "cro_wall1-2";
		delwall "cro_wall1-3";
		delwall "cro_wall1-4";
		mapannounce "bat_b01","크루아 진영의 크리스탈 공격 가능!",1,0xFF0000;
	}
	end;

OnGuillaumeBreak:
	donpcevent "Flavius_BG1::OnRoundStop";
	set .Guillaume_Loss, .Guillaume_Loss + 1;
	if( set(.Croix_Score, .Croix_Score + 1) < 2 )
	{
		bg_updatescore "bat_b01",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b01","기욤 진영의 크리스탈이 파괴되었습니다!",1,0x0000FF;
		donpcevent "Flavius_BG1::OnRoundStart";
	}
	else
	{
		bg_updatescore "bat_b01",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b01","크루아 군이 플라비우스 전투에서 승리하였습니다!",1,0xFF0000;
		set $@FlaviusBG1_Victory, 2;
		donpcevent "Flavius_BG1::OnMatchEnd";
	}
	end;

OnCroixBreak:
	donpcevent "Flavius_BG1::OnRoundStop";
	set .Croix_Loss, .Croix_Loss + 1;
	if( set(.Guillaume_Score, .Guillaume_Score + 1) < 2 )
	{
		bg_updatescore "bat_b01",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b01","크루아 진영의 크리스탈이 파괴되었습니다!",1,0xFF0000;
		donpcevent "Flavius_BG1::OnRoundStart";
	}
	else
	{
		bg_updatescore "bat_b01",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b01","기욤 군이 플라비우스 전투에서 승리하였습니다!",1,0x0000FF;
		set $@FlaviusBG1_Victory, 1;
		donpcevent "Flavius_BG1::OnMatchEnd";
	}
	end;

OnTimer2400000:
	mapannounce "bat_b01","플라비우스 전투가 5분 후에 종료됩니다.",1,0x006400;
	end;
OnTimer2640000:
	mapannounce "bat_b01","플라비우스 전투가 1분 후에 종료됩니다.",1,0x006400;
	end;

OnTimer2700000:
	if( .Croix_Score > .Guillaume_Score )
	{
		mapannounce "bat_b01","크루아 군이 플라비우스 전투에서 승리하였습니다!",1,0xFF0000;
		set $@FlaviusBG1_Victory, 2;
	}
	else if( .Croix_Score < .Guillaume_Score )
	{
		mapannounce "bat_b01","기욤 군이 플라비우스 전투에서 승리하였습니다!",1,0x0000FF;
		set $@FlaviusBG1_Victory, 1;
	}
	else
	{
		mapannounce "bat_b01","플라비우스 전투가 무승부로 종료되었습니다.",1,0x006400;
		set $@FlaviusBG1_Victory, 3;
	}

OnMatchEnd:
	set $@FlaviusBG1, 2;
	stopnpctimer;
	donpcevent "#gfl1_respawn::OnBGStop";
	donpcevent "#cfl1_respawn::OnBGStop";
	disablenpc "전장의 치료사#fl11";
	disablenpc "전장의 치료사#fl12";
	enablenpc "기욤 이십인장#fl1";
	enablenpc "크루아 이십인장#fl1";
	sleep 2000;
	bg_warp $@FlaviusBG1_id1,"bat_b01",390,10;
	bg_warp $@FlaviusBG1_id2,"bat_b01",10,290;
	sleep 3000;
	mapannounce "bat_b01","플라비우스 전장이 1분 후에 닫힙니다!",1,0x006400;
	initnpctimer;
	end;

OnTimer30000:
	if( $@FlaviusBG1 == 2 )
		mapannounce "bat_b01","플라비우스 전장이 30초 후에 닫힙니다!",1,0x006400;
	end;
OnTimer50000:
	if( $@FlaviusBG1 == 2 )
		mapannounce "bat_b01","플라비우스 전장이 10초 후에 닫힙니다!",1,0x006400;
	end;

OnTimer60000:
	if( $@FlaviusBG1 != 2 )
		end;
OnReset:
	stopnpctimer;
	donpcevent "Flavius_BG1::OnRoundStop";
	set .Guillaume_Score, 0;
	set .Guillaume_Crystal, 0;
	set .Guillaume_Loss, 0;
	set .Croix_Score, 0;
	set .Croix_Crystal, 0;
	set .Croix_Loss, 0;
	set .Match, 0;
	set $@FlaviusBG1_Victory, 0;
	// NPC's
	disablenpc "기욤 이십인장#fl1";
	disablenpc "크루아 이십인장#fl1";
	disablenpc "전장의 치료사#fl11";
	disablenpc "전장의 치료사#fl12";

	if( $@FlaviusBG1_id1 ) { bg_destroy $@FlaviusBG1_id1; set $@FlaviusBG1_id1, 0; }
	if( $@FlaviusBG1_id2 ) { bg_destroy $@FlaviusBG1_id2; set $@FlaviusBG1_id2, 0; }
	sleep 1000;
	mapwarp "bat_b01","bat_room",155,150;
	sleep 2000;
	maprespawnguildid "bat_b01",0,3; // Just in case someone else
	sleep 2000;
	bg_updatescore "bat_b01",0,0;
	set $@FlaviusBG1, 0;
	donpcevent "Flavius_BG1::OnReadyCheck";
	end;
}

// Battleground rewards
// *********************************************************************

bat_b01,390,13,5	script	기욤 이십인장#fl1	419,{
	if( $@FlaviusBG1_Victory )
	{
		if( $@FlaviusBG1_Victory == Bat_Team )
		{ // Victory
			mes "[이십인장]";
			mes "기욤에게 축복을!!";
			mes "우리의 영광스러운 승리를 즐깁시다!";
			mes "" + strcharinfo(0) + ", 이것은 승리의 징표요!";
			close2;
			set .@Reward, 10;
			set .@Reward2, 10;
		}
		else
		{ // 
			mes "[이십인장]";
			mes "당신은 패배하였지만, 이 전투에 참가하였소.";
			mes "이것은 기욤 마롤로를 도와 전투에 참가한 것에 대한 보상이오!";
			mes "이 패배를 교훈으로 삼아, 당신은 다음 번에는 반드시 승리할 것이오!";
			close2;
			set .@Reward, 7;
			set .@Reward, 7;
		}

		set Flavius_BG_Tick, gettimetick(2) + 150;
		getitem 7829, .@Reward;
		getitem 671, .@Reward2;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}

bat_b01,10,293,5	script	크루아 이십인장#fl1	415,{
	if( $@FlaviusBG1_Victory )
	{
		if( $@FlaviusBG1_Victory == Bat_Team )
		{ // Victory
			mes "[이십인장]";
			mes "크루아에게 축복을!!";
			mes "우리의 영광스러운 승리를 즐깁시다!";
			mes "" + strcharinfo(0) + ", 이것은 승리의 징표요!";
			close2;
			set .@Reward, 10;
			set .@Reward2, 10;
		}
		else
		{ // 
			mes "[이십인장]";
			mes "오, " + strcharinfo(0) + ". 슬퍼하지 말게나.";
			mes "우리가 비록 이기지 못했지만, 우리는 최선을 다했네.";
			mes "이것은 크루아 님으로부터의 선물이네. 그리고, 이 전투를 잊지 말게나. 우리는 다음 번에는 승리할 것일세.";
			close2;
			set .@Reward, 7;
			set .@Reward, 7;
		}

		set Flavius_BG_Tick, gettimetick(2) + 150;
		getitem 7829, .@Reward;
		getitem 671, .@Reward2;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}

// Battleground Therapist
// *********************************************************************

bat_b01,390,13,5	script	전장의 치료사#fl12	95,{
	mes "[전장의 치료사]";
	mes "잠시 눈을 감고, 깊게 숨을 들이쉬세요.";
	mes "당신은 고통으로부터 자유로워져야 합니다.";
	specialeffect2 312; percentheal 100,100;
	specialeffect2 EF_INCAGILITY; sc_start SC_INCREASEAGI,240000,10;
	specialeffect2 EF_BLESSING; sc_start SC_BLESSING,240000,10;
	close;
}

bat_b01,10,293,5	script	전장의 치료사#fl11	95,{
	mes "[전장의 치료사]";
	mes "잠시 눈을 감고, 깊게 숨을 들이쉬세요.";
	mes "당신은 고통으로부터 자유로워져야 합니다.";
	specialeffect2 312; percentheal 100,100;
	specialeffect2 EF_INCAGILITY; sc_start SC_INCREASEAGI,240000,10;
	specialeffect2 EF_BLESSING; sc_start SC_BLESSING,240000,10;
	close;
}

// Battleground Respawn
// *********************************************************************

bat_b01,390,10,0	script	#gfl1_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b01",382,2,397,17,100,100;
	areawarp "bat_b01",382,2,397,17,"bat_b01",311,224;
	initnpctimer;
	end;
}

bat_b01,10,290,0	script	#cfl1_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b01",2,282,17,297,100,100;
	areawarp "bat_b01",2,282,17,297,"bat_b01",87,75;
	initnpctimer;
	end;
}

// Flags
// *********************************************************************

bat_b01,304,231,1	duplicate(기욤 진영#bat)	기욤 진영#bat1	973
bat_b01,319,231,1	duplicate(기욤 진영#bat)	기욤 진영#bat2	973
bat_b01,304,218,1	duplicate(기욤 진영#bat)	기욤 진영#bat3	973
bat_b01,319,218,1	duplicate(기욤 진영#bat)	기욤 진영#bat4	973
bat_b01,304,231,1	duplicate(기욤 진영#bat)	기욤 진영#bat5	973
bat_b01,304,231,1	duplicate(기욤 진영#bat)	기욤 진영#bat6	973
bat_b01,335,142,1	duplicate(기욤 진영#bat)	기욤 진영#bat7	973
bat_b01,335,157,1	duplicate(기욤 진영#bat)	기욤 진영#bat8	973
bat_b01,390,16,1	duplicate(기욤 진영#bat)	기욤 진영#bat9	973
bat_b01,292,163,1	duplicate(기욤 진영#bat)	기욤 진영#bat19	973
bat_b01,292,136,1	duplicate(기욤 진영#bat)	기욤 진영#bat20	973
bat_b01,241,185,1	duplicate(기욤 진영#bat)	기욤 진영#bat21	973
bat_b01,247,179,1	duplicate(기욤 진영#bat)	기욤 진영#bat22	973

bat_b01,96,81,1	duplicate(크루아 진영#bat)	크루아 진영#bat1	974
bat_b01,96,68,1	duplicate(크루아 진영#bat)	크루아 진영#bat2	974
bat_b01,79,81,1	duplicate(크루아 진영#bat)	크루아 진영#bat3	974
bat_b01,79,68,1	duplicate(크루아 진영#bat)	크루아 진영#bat4	974
bat_b01,96,81,1	duplicate(크루아 진영#bat)	크루아 진영#bat5	974
bat_b01,96,81,1	duplicate(크루아 진영#bat)	크루아 진영#bat6	974
bat_b01,59,164,1	duplicate(크루아 진영#bat)	크루아 진영#bat7	974
bat_b01,59,137,1	duplicate(크루아 진영#bat)	크루아 진영#bat8	974
bat_b01,10,296,1	duplicate(크루아 진영#bat)	크루아 진영#bat9	974
bat_b01,110,162,1	duplicate(크루아 진영#bat)	크루아 진영#bat18	974
bat_b01,110,137,1	duplicate(크루아 진영#bat)	크루아 진영#bat19	974
bat_b01,152,120,1	duplicate(크루아 진영#bat)	크루아 진영#bat20	974
bat_b01,158,114,1	duplicate(크루아 진영#bat)	크루아 진영#bat21	974

// MapFlags
// *********************************************************************

bat_b01	mapflag	battleground	2
bat_b01	mapflag	nomemo
bat_b01	mapflag	nosave	SavePoint
bat_b01	mapflag	noteleport
bat_b01	mapflag	nowarp
bat_b01	mapflag	nowarpto
bat_b01	mapflag	noreturn
bat_b01	mapflag	nobranch
bat_b01	mapflag	nopenalty
