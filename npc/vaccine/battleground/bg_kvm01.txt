bat_room,253,204,0	script	대기실#KvM01R_Croix	414,{ // KvM Croix
	end;
	
OnKvMWait:
	delwaitingroom;
	waitingroom "KvM 전투 대기실 - "+$KvMP+" 명",$KvMP+1,"KvM01_BG::OnCroixJoin",1;
	end;
	
OnKvMNoin:
	delwaitingroom;
	waitingroom "KvM 전투 대기실 - 인원 조정중",0;
	end;

OnInit:
	sleep 2000;
	waitingroom "KvM 전투 대기실 - "+$KvMP+" 명",$KvMP+1,"KvM01_BG::OnCroixJoin",1;
	end;

OnEnterBG:
	set $@KvM01BG_id2, waitingroom2bg("bat_c01",146,55,"KvM01_BG::OnCroixQuit","KvM01_BG::OnCroixDie");
	end;
}
bat_room,253,227,4	script	대기실#KvM01R_Guillaume	418,{ // KvM Guillaume
	end;
	
OnKvMWait:
	delwaitingroom;
	waitingroom "KvM 전투 대기실 - "+$KvMP+" 명",$KvMP+1,"KvM01_BG::OnGuillaumeJoin",1;
	end;

OnKvMNoin:
	delwaitingroom;
	waitingroom "KvM 전투 대기실 - 인원 조정중",0;
	end;
	
OnInit:
	waitingroom "KvM 전투 대기실 - "+$KvMP+" 명",$KvMP+1,"KvM01_BG::OnGuillaumeJoin",1;
	end;

OnEnterBG:
	set $@KvM01BG_id1, waitingroom2bg("bat_c01",53,128,"KvM01_BG::OnGuillaumeQuit","KvM01_BG::OnGuillaumeDie");
	end;
}
// Light Effects
// *********************************************************************

bat_c01,54,123,0	script	#bat_c01a	111,{
	end;

OnKvM01LightA: specialeffect EF_BEGINSPELL2; end;
OnKvM01LightB: specialeffect EF_BEGINSPELL3; end;
OnKvM01LightC: specialeffect EF_BEGINSPELL4; end;
}

bat_c01,56,125,0	duplicate(#bat_c01a)	#bat_c01b	111
bat_c01,58,127,0	duplicate(#bat_c01a)	#bat_c01c	111
bat_c01,141,56,0	duplicate(#bat_c01a)	#bat_c01d	111
bat_c01,143,58,0	duplicate(#bat_c01a)	#bat_c01e	111
bat_c01,145,60,0	duplicate(#bat_c01a)	#bat_c01f	111

// Battleground Engine
// *********************************************************************

-	script	KvM01_BG	-1,{
	end;

OnInit:
	setwall "bat_c01",54,122,6,7,0,"batc01wall_a";
	setwall "bat_c01",55,122,5,7,0,"batc01wall_b";
	setwall "bat_c01",140,56,6,7,0,"batc01wall_c";
	setwall "bat_c01",140,57,5,7,0,"batc01wall_d";
	disablenpc "TherapistKvM01a";
	disablenpc "TherapistKvM01b";
	disablenpc "VintenarKvM01a";
	disablenpc "VintenarKvM01b";
	end;

OnGuillaumeJoin:
OnCroixJoin:
	donpcevent "KvM01_BG::OnReadyCheck";
	end;

OnGuillaumeQuit:
	set BG_Delay_Tick, gettimetick(2) + 1200;
OnGuillaumeDie:
	if( $@KvM01BG == 2 )
	{
		set .Croix_Score, .Croix_Score + 1;
		set @kvm_die, 0;
		bg_updatescore "bat_c01",.Guillaume_Score,.Croix_Score;
		if( .Guillaume_Count < 1 ) donpcevent "KvM01_BG::OnCroixWin";
	}
	end;

OnCroixQuit:
	set BG_Delay_Tick, gettimetick(2) + 1200;
OnCroixDie:
	if( $@KvM01BG == 2 )
	{
		set .Guillaume_Score, .Guillaume_Score + 1;
		set @kvm_die, 0;
		bg_updatescore "bat_c01",.Guillaume_Score,.Croix_Score;
		if( .Croix_Count < 1 ) donpcevent "KvM01_BG::OnGuillaumeWin";
	}
	end;

OnReadyCheck:
	if( $@KvM01BG )
		end;
	set .@Guillaume, getwaitingroomstate(0,"대기실#KvM01R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"대기실#KvM01R_Croix");

	if( .@Guillaume < $KvMP || .@Croix < $KvMP )
	{
		if( .@Guillaume > 3 && .@Croix > 3 && !agitcheck() && !agitcheck2() && $@KvM_Flood < gettimetick(2) )
		{
			announce "전장 -- KVM [80-99] 기욤 진영: " + .@Guillaume + "/$KvMP, 크루아 진영: " + .@Croix + "/$KvMP",0,0x696969;
			set $@KvM_Flood, gettimetick(2) + 15;
		}
		else
			mapannounce "bat_room","전장 -- KVM [80-99] 기욤 진영: " + .@Guillaume + "/"+$KvMP+" 명, 크루아 진영: " + .@Croix + "/"+$KvMP+" 명",1,0x696969;
		end;
	}

	set $@KvM01BG, 1; // Starting
	donpcevent "대기실#KvM01R_Croix::OnEnterBG";
	donpcevent "대기실#KvM01R_Guillaume::OnEnterBG";
	donpcevent "KvM01_BG::OnStart";
	end;

OnStart:
	announce "전장 -- KVM [80-99]이 시작됩니다!",0,0x696969;
	enablenpc "TherapistKvM01a";
	enablenpc "TherapistKvM01b";
	disablenpc "VintenarKvM01a";
	disablenpc "VintenarKvM01b";
	set $@KvM01BG_Victory, 0;
	sleep 2000;
	// Warp Teams
	bg_warp $@KvM01BG_id1,"bat_c01",53,128;
	bg_warp $@KvM01BG_id2,"bat_c01",146,55;
	// ScoreBoard
	set .Guillaume_Score, 0;
	set .Croix_Score, 0;
	initnpctimer;
	end;

OnTimer5000:
	areapercentheal "bat_c01",50,123,58,131,100,100;
	areapercentheal "bat_c01",141,52,149,60,100,100;
	mapannounce "bat_c01","10초 후 전투가 시작됩니다!!",1,0x696969;
	end;

OnTimer16000:
	mapannounce "bat_c01","4초 후 전투가 시작됩니다!!",1,0x696969;
	end;

OnTimer17000:
	donpcevent "::OnKvM01LightA";
	end;

OnTimer18000:
	donpcevent "::OnKvM01LightB";
	end;
	
OnTimer19000:
	donpcevent "::OnKvM01LightC";
	end;

OnTimer20000:
	// Team Members
	set .Guillaume_Count, bg_get_data($@KvM01BG_id1, 0);
	set .Croix_Count, bg_get_data($@KvM01BG_id2, 0);
	if( .Guillaume_Count < $KvMP || .Croix_Count < $KvMP ) //★인원 [전투 시작시 최종 인원체크]
	{
		set $@KvM01BG_Victory, 3;
		set $@KvM01BG, 3;
		mapannounce "bat_c01","인원이 맞지 않아 전장이 종료됩니다.",1,0x696969;
		stopnpctimer;
		sleep 2000;
		donpcevent "KvM01_BG::OnStop";
		end;
	}

	set $@KvM01BG, 2; // Playing
	areapercentheal "bat_c01",50,123,58,131,100,100;
	bg_warp $@KvM01BG_id1,"bat_c01",62,119;
	areapercentheal "bat_c01",141,52,149,60,100,100;
	bg_warp $@KvM01BG_id2,"bat_c01",137,64;
	end;

OnTimer32000:
	mapannounce "bat_c01","KVM 전투가 시작되었습니다!!",1,0x696969;
	end;

OnTimer300000:
	mapannounce "bat_c01","30초 후 전투가 종료됩니다!!",1,0x696969;
	end;

OnTimer330000:
	if( .Croix_Score > .Guillaume_Score )
		donpcevent "KvM01_BG::OnCroixWin";
	else if( .Croix_Score < .Guillaume_Score )
		donpcevent "KvM01_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@KvM01BG, 3;
		set $@KvM01BG_Victory, 3;

		stopnpctimer;
		sleep 2000;
		mapannounce "bat_c01","이번 전투는 무승부입니다.",1,0x696969;
		donpcevent "KvM01_BG::OnStop";
	}
	end;

OnGuillaumeWin:
	set $@KvM01BG, 3;
	set $@KvM01BG_Victory, 1;
	
	stopnpctimer;
	sleep 2000;
	mapannounce "bat_c01","기욤 진영의 승리입니다.",1,0x0000FF;
	donpcevent "KvM01_BG::OnStop";
	end;

OnCroixWin:
	set $@KvM01BG, 3;
	set $@KvM01BG_Victory, 2;

	stopnpctimer;
	sleep 2000;
	mapannounce "bat_c01","크루아 진영의 승리입니다.",1,0xFF0000;
	donpcevent "KvM01_BG::OnStop";
	end;

OnStop:
	disablenpc "TherapistKvM01a";
	disablenpc "TherapistKvM01b";
	enablenpc "VintenarKvM01a";
	enablenpc "VintenarKvM01b";
	// Warp Teams
	bg_warp $@KvM01BG_id1,"bat_c01",53,128;
	bg_warp $@KvM01BG_id2,"bat_c01",146,55;
	donpcevent "KvM01_BG_Out::OnBegin";
	end;

OnReset:
	stopnpctimer;
	stopnpctimer "KvM01_BG_Out";
	set .Croix_Count, 0;
	set .Guillaume_Count, 0;
	set .Croix_Score, 0;
	set .Guillaume_Score, 0;
	set $@KvM01BG_Victory, 0;
	if( $@KvM01BG_id1 ) { bg_destroy $@KvM01BG_id1; set $@KvM01BG_id1, 0; }
	if( $@KvM01BG_id2 ) { bg_destroy $@KvM01BG_id2; set $@KvM01BG_id2, 0; }
	disablenpc "TherapistKvM01a";
	disablenpc "TherapistKvM01b";
	disablenpc "VintenarKvM01a";
	disablenpc "VintenarKvM01b";
	sleep 1000;
	mapwarp "bat_c01","bat_room",155,150;
	sleep 2000;
	maprespawnguildid "bat_c01",0,3; // Just in case someone else
	bg_updatescore "bat_c01",0,0;
	sleep 2000;
	set $@KvM01BG, 0;
	donpcevent "KvM01_BG::OnReadyCheck"; // Maybe a game is ready to start
	end;
}

-	script	KvM01_BG_Out	-1,{
	end;

OnBegin:
	initnpctimer;
	end;

OnTimer3000:
	mapannounce "bat_c01","1분 후 전장이 초기화됩니다.!",1,0x696969;
	end;
OnTimer30000:
	mapannounce "bat_c01","30초 후 전장이 초기화됩니다.!",1,0x696969;
	end;
OnTimer50000:
	mapannounce "bat_c01","10초 후 전장이 초기화됩니다.!",1,0x696969;
	end;
OnTimer60000:
	donpcevent "KvM01_BG::OnReset";
	end;
}

// Battleground Therapist
// *********************************************************************

bat_c01,51,130,5	script	전장 사제::TherapistKvM01a	95,{
	if($@KvM01BG != 2) end;
	if((Hp / MaxHp * 100) > 10) end;
	if(@kvm_die == 1) end;
	mes "[전장 사제]";
	mes "신의 가호가 함께 하기를....";
	specialeffect2 EF_HEAL2; percentheal 100,100;
	specialeffect2 EF_INCAGILITY; sc_start SC_INCREASEAGI,240000,10;
	specialeffect2 EF_BLESSING; sc_start SC_BLESSING,240000,10;
	set @kvm_die, 1;
	next;
			warp "bat_c01",61,120;
	close2;
}

bat_c01,148,53,1	script	전장 사제::TherapistKvM01b	95,{
	if($@KvM01BG != 2) end;
	if((Hp / MaxHp * 100) > 10) end;
	if(@kvm_die == 1) end;
	mes "[전장 사제]";
	mes "신의 가호가 함께 하기를....";
	specialeffect2 EF_HEAL2; percentheal 100,100;
	specialeffect2 EF_INCAGILITY; sc_start SC_INCREASEAGI,240000,10;
	specialeffect2 EF_BLESSING; sc_start SC_BLESSING,240000,10;
	set @kvm_die, 1;
	next;
			warp "bat_c01",138,63;
	close2;
}

// Battleground rewards
// *********************************************************************

bat_c01,51,130,5	script	기욤 보상::VintenarKvM01a	419,{
	if( $@KvM01BG_Victory )
	{
		if( $@KvM01BG_Victory == 3 )
		{
			setquest 6025;
			bg_leave;
			warp "bat_room",155,150;
			end;
		}
		else if( $@KvM01BG_Victory == Bat_Team )
		{ // Victory
			set .@Reward, 5;
			set .@Reward2, 10;
			mes "[기욤 보상]";
			mes "기욤의 영광이!!";
			mes "당신을 승리로 이끌었군요!";
			mes "" + strcharinfo(0) + "님! 이건 승리 보상입니다.";
			close2;
		}
		else
		{ // 
			set .@Reward, 3;
			set .@Reward2, 7;
			mes "[기욤 보상]";
			mes "아쉽게 패배했군요....";
			mes "그래도 위대한 기욤 마롤로 장군님께서 하사하신 선물입니다.";
			close2;
		}

		setquest 6025;
		getitem 7773, .@Reward;
		getitem 671, .@Reward2;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}

bat_c01,148,53,1	script	크루아 보상::VintenarKvM01b	415,{
	if( $@KvM01BG_Victory )
	{
		if( $@KvM01BG_Victory == 3 )
		{
			setquest 6025;
			bg_leave;
			warp "bat_room",155,150;
			end;
		}
		else if( $@KvM01BG_Victory == Bat_Team )
		{ // Victory
			set .@Reward, 5;
			set .@Reward2, 10;
			mes "[크루아 보상]";
			mes "크루아의 영광이!!";
			mes "당신을 승리로 이끌었군요!";
			mes "" + strcharinfo(0) + "님! 이건 승리 보상입니다.";
			close2;
		}
		else
		{ // 
			set .@Reward, 3;
			set .@Reward2, 7;
			mes "[크루아 보상]";
			mes "오, " + strcharinfo(0) + "님. 슬퍼하지마세요..";
			mes "다음에 승리를 위한 연습이였다고 생각하시고.";
			mes "이건 크루아님이 주신 선물입니다..";
			close2;
		}

		setquest 6025;
		getitem 7773, .@Reward;
		getitem 671, .@Reward2;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}
