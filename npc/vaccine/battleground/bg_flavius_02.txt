// ==============================================================================
// BattleGround System - Flavius 2
// ==============================================================================

// Registration NPC's
// *********************************************************************

bat_room,57,97,4	script	Registration::Fl2R_Guillaume	418,{
	end;
OnInit:
	waitingroom "전투 대기실 - "+$Flavi+" 명",$Flavi+1,"Flavius_BG2::OnGuillaumeJoin",1;
	end;
OnEnterBG:
	set $@FlaviusBG2_id1, waitingroom2bg("bat_b02",390,10,"Flavius_BG2::OnGuillaumeQuit","");
	end;
OnFlaNoin:
	delwaitingroom;
	waitingroom "전투 대기실 - 인원 조정중",0;
	end;
OnFlaWait:
	delwaitingroom;
	waitingroom "전투 대기실 - "+$Flavi+" 명",$Flavi+1,"Flavius_BG2::OnGuillaumeJoin",1;
	end;
}

bat_room,57,74,0	script	Registration::Fl2R_Croix	414,{
	end;
OnInit:
	waitingroom "전투 대기실 - "+$Flavi+" 명",$Flavi+1,"Flavius_BG2::OnCroixJoin",1;
	end;
OnEnterBG:
	set $@FlaviusBG2_id2, waitingroom2bg("bat_b02",10,290,"Flavius_BG2::OnCroixQuit","");
	end;
OnFlaNoin:
	delwaitingroom;
	waitingroom "전투 대기실 - 인원 조정중",0;
	end;
OnFlaWait:
	delwaitingroom;
	waitingroom "전투 대기실 - "+$Flavi+" 명",$Flavi+1,"Flavius_BG2::OnCroixJoin",1;
	end;
}

// Battleground Engine
// *********************************************************************

-	script	Flavius_BG2	-1,{
	end;

OnInit:
	disablenpc "기욤 이십인장#fl2";
	disablenpc "크루아 이십인장#fl2";
	disablenpc "전장의 치료사#fl21";
	disablenpc "전장의 치료사#fl22";
	end;

OnGuillaumeQuit:
OnCroixQuit:
	set BG_Delay_Tick, gettimetick(2) + 600;
	end;

OnGuillaumeJoin:
OnCroixJoin:
	if( $@FlaviusBG2 == 0 )
		donpcevent "Flavius_BG2::OnReadyCheck";
	end;

OnReadyCheck:
	if( $@FlaviusBG2 )
		end;
	set .@Guillaume, getwaitingroomstate(0,"Fl2R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"Fl2R_Croix");
	
	if( .@Guillaume < $Flavi || .@Croix < $Flavi || .@Guillaume != .@Croix )
	{
		mapannounce "bat_room","전장 - 플라비우스 전투 기욤 진영 : 현재 " + .@Guillaume + "/"+$Flavi+"명, 크루아 진영 : 현재 " + .@Croix + "/"+$Flavi+"명",1,0x808000;
		end;
	}

	sleep 10000;
	
	set .@Guillaume_R, getwaitingroomstate(0,"Fl2R_Guillaume"); 
	set .@Croix_R, getwaitingroomstate(0,"Fl2R_Croix"); 

	if( .@Guillaume_R != .@Croix_R ) 
	{ 
		donpcevent "Flavius_BG2::OnReadyCheck"; 
		end; 
	} 
	
	// BG Variables
	set $@FlaviusBG2, 1;
	set $@FlaviusBG2_Victory, 0;
	set .Guillaume_Score, 0;
	set .Guillaume_Loss, 0;
	set .Croix_Score, 0;
	set .Croix_Loss, 0;
	set .Match, 0;
	// Prepare NPC
	donpcevent "#gfl2_respawn::OnBGStart";
	donpcevent "#cfl2_respawn::OnBGStart";
	enablenpc "전장의 치료사#fl21";
	enablenpc "전장의 치료사#fl22";
	disablenpc "기욤 이십인장#fl2";
	disablenpc "크루아 이십인장#fl2";
	// Build and Warp Teams
	donpcevent "Fl2R_Guillaume::OnEnterBG";
	donpcevent "Fl2R_Croix::OnEnterBG";
	announce "전장 - 플라비우스 전투가 시작되었습니다!",0,0x808000;
	initnpctimer;
	// Start Match!!

OnRoundStart:
	sleep 2000;
	if( $@FlaviusBG2 != 1 ) end;

	areapercentheal "bat_b02",382,2,397,17,100,100;
	areapercentheal "bat_b02",2,282,17,297,100,100;
	bg_warp $@FlaviusBG2_id1,"bat_b02",311,224;
	bg_warp $@FlaviusBG2_id2,"bat_b02",87,75;

	sleep 2000;
	if( $@FlaviusBG2 != 1 ) end;

	set .Match, .Match + 1;
	// Crystal Spawn
	set .Guillaume_Crystal, bg_monster($@FlaviusBG2_id1,"bat_b02",328,150,"기욤 크리스탈",1914,"Flavius_BG2::OnGuillaumeBreak");
	setwall "bat_b02",327,149,2,6,0,"gui_wall2-1";
	setwall "bat_b02",329,149,2,0,0,"gui_wall2-2";
	setwall "bat_b02",329,151,2,2,0,"gui_wall2-3";
	setwall "bat_b02",327,151,2,4,0,"gui_wall2-4";

	set .Croix_Crystal, bg_monster($@FlaviusBG2_id2,"bat_b02",62,150,"크루아 크리스탈",1915,"Flavius_BG2::OnCroixBreak");
	setwall "bat_b02",61,149,2,6,0,"cro_wall2-1";
	setwall "bat_b02",63,149,2,0,0,"cro_wall2-2";
	setwall "bat_b02",63,151,2,2,0,"cro_wall2-3";
	setwall "bat_b02",61,151,2,4,0,"cro_wall2-4";

	// Guardian Spawns
	bg_monster $@FlaviusBG2_id1,"bat_b02",328,160,"기욤 가디언",1949,"Flavius_BG2::OnGuiGuardian";
	bg_monster $@FlaviusBG2_id1,"bat_b02",328,140,"기욤 가디언",1950,"Flavius_BG2::OnGuiGuardian";
	set .Guillaume_Guardian, 2;
	bg_monster $@FlaviusBG2_id2,"bat_b02",62,160,"크루아 가디언",1949,"Flavius_BG2::OnCroGuardian";
	bg_monster $@FlaviusBG2_id2,"bat_b02",61,140,"크루아 가디언",1950,"Flavius_BG2::OnCroGuardian";
	set .Croix_Guardian, 2;
	// Announces
	mapannounce "bat_b02","플라비우스 전장 - [ " + .Match + " ] 번째 라운드가 시작되었습니다!",1,0x808000;
	end;

OnRoundStop:
	// Remove Monsters
	killmonster "bat_b02","Flavius_BG2::OnGuiGuardian";
	killmonster "bat_b02","Flavius_BG2::OnCroGuardian";
	delwall "gui_wall2-1";
	delwall "gui_wall2-2";
	delwall "gui_wall2-3";
	delwall "gui_wall2-4";
	killmonster "bat_b02","Flavius_BG2::OnGuillaumeBreak";
	delwall "cro_wall2-1";
	delwall "cro_wall2-2";
	delwall "cro_wall2-3";
	delwall "cro_wall2-4";
	killmonster "bat_b02","Flavius_BG2::OnCroixBreak";
	end;

OnGuiGuardian:
	if( set(.Guillaume_Guardian, .Guillaume_Guardian - 1) <= 0 )
	{
		delwall "gui_wall2-1";
		delwall "gui_wall2-2";
		delwall "gui_wall2-3";
		delwall "gui_wall2-4";
		mapannounce "bat_b02","기욤 진영의 크리스탈 공격 가능!",1,0x0000FF;
	}
	end;

OnCroGuardian:
	if( set(.Croix_Guardian, .Croix_Guardian - 1) <= 0 )
	{
		delwall "cro_wall2-1";
		delwall "cro_wall2-2";
		delwall "cro_wall2-3";
		delwall "cro_wall2-4";
		mapannounce "bat_b02","크루아 진영의 크리스탈 공격 가능!",1,0xFF0000;
	}
	end;

OnGuillaumeBreak:
	donpcevent "Flavius_BG2::OnRoundStop";
	set .Guillaume_Loss, .Guillaume_Loss + 1;
	if( set(.Croix_Score, .Croix_Score + 1) < 2 )
	{
		bg_updatescore "bat_b02",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b02","기욤 진영의 크리스탈이 파괴되었습니다!",1,0x0000FF;
		donpcevent "Flavius_BG2::OnRoundStart";
	}
	else
	{
		bg_updatescore "bat_b02",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b02","크루아 군이 플라비우스 전투에서 승리하였습니다!",1,0xFF0000;
		set $@FlaviusBG2_Victory, 2;
		donpcevent "Flavius_BG2::OnMatchEnd";
	}
	end;

OnCroixBreak:
	donpcevent "Flavius_BG2::OnRoundStop";
	set .Croix_Loss, .Croix_Loss + 1;
	if( set(.Guillaume_Score, .Guillaume_Score + 1) < 2 )
	{
		bg_updatescore "bat_b02",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b02","크루아 진영의 크리스탈이 파괴되었습니다!",1,0xFF0000;
		donpcevent "Flavius_BG2::OnRoundStart";
	}
	else
	{
		bg_updatescore "bat_b02",.Guillaume_Score,.Croix_Score;
		mapannounce "bat_b02","기욤 군이 플라비우스 전투에서 승리하였습니다!",1,0x0000FF;
		set $@FlaviusBG2_Victory, 1;
		donpcevent "Flavius_BG2::OnMatchEnd";
	}
	end;

OnTimer2400000:
	mapannounce "bat_b02","플라비우스 전투가 5분 후에 종료됩니다.",1,0x808000;
	end;
OnTimer2640000:
	mapannounce "bat_b02","플라비우스 전투가 1분 후에 종료됩니다.",1,0x808000;
	end;

OnTimer2700000:
	if( .Croix_Score > .Guillaume_Score )
	{
		mapannounce "bat_b02","크루아 군이 플라비우스 전투에서 승리하였습니다!",1,0xFF0000;
		set $@FlaviusBG2_Victory, 2;
	}
	else if( .Croix_Score < .Guillaume_Score )
	{
		mapannounce "bat_b02","기욤 군이 플라비우스 전투에서 승리하였습니다!",1,0x0000FF;
		set $@FlaviusBG2_Victory, 1;
	}
	else
	{
		mapannounce "bat_b02","플라비우스 전투가 무승부로 종료되었습니다.",1,0x808000;
		set $@FlaviusBG2_Victory, 3;
	}

OnMatchEnd:
	set $@FlaviusBG2, 2;
	stopnpctimer;
	donpcevent "#gfl2_respawn::OnBGStop";
	donpcevent "#cfl2_respawn::OnBGStop";
	disablenpc "전장의 치료사#fl21";
	disablenpc "전장의 치료사#fl22";
	enablenpc "기욤 이십인장#fl2";
	enablenpc "크루아 이십인장#fl2";
	sleep 2000;
	bg_warp $@FlaviusBG2_id1,"bat_b02",390,10;
	bg_warp $@FlaviusBG2_id2,"bat_b02",10,290;
	sleep 3000;
	mapannounce "bat_b02","플라비우스 전장이 1분 후에 닫힙니다!",1,0x808000;
	initnpctimer;
	end;

OnTimer30000:
	if( $@FlaviusBG2 == 2 )
		mapannounce "bat_b02","플라비우스 전장이 30초 후에 닫힙니다!",1,0x808000;
	end;
OnTimer50000:
	if( $@FlaviusBG2 == 2 )
		mapannounce "bat_b02","플라비우스 전장이 10초 후에 닫힙니다!",1,0x808000;
	end;

OnTimer60000:
	if( $@FlaviusBG2 != 2 )
		end;
OnReset:
	stopnpctimer;
	donpcevent "Flavius_BG2::OnRoundStop";
	set .Guillaume_Score, 0;
	set .Guillaume_Crystal, 0;
	set .Guillaume_Loss, 0;
	set .Croix_Score, 0;
	set .Croix_Crystal, 0;
	set .Croix_Loss, 0;
	set .Match, 0;
	set $@FlaviusBG2_Victory, 0;
	// NPC's
	disablenpc "기욤 이십인장#fl2";
	disablenpc "크루아 이십인장#fl2";
	disablenpc "전장의 치료사#fl21";
	disablenpc "전장의 치료사#fl22";

	if( $@FlaviusBG2_id1 ) { bg_destroy $@FlaviusBG2_id1; set $@FlaviusBG2_id1, 0; }
	if( $@FlaviusBG2_id2 ) { bg_destroy $@FlaviusBG2_id2; set $@FlaviusBG2_id2, 0; }
	sleep 1000;
	mapwarp "bat_b02","bat_room",155,150;
	sleep 2000;
	maprespawnguildid "bat_b02",0,3; // Just in case someone else
	sleep 2000;
	bg_updatescore "bat_b02",0,0;
	set $@FlaviusBG2, 0;
	donpcevent "Flavius_BG2::OnReadyCheck";
	end;
}

// Battleground rewards
// *********************************************************************

bat_b02,390,13,5	script	기욤 이십인장#fl2	419,{
	if( $@FlaviusBG2_Victory )
	{
		if( $@FlaviusBG2_Victory == Bat_Team )
		{ // Victory
			mes "[이십인장]";
			mes "기욤에게 축복을!!";
			mes "우리의 영광스러운 승리를 즐깁시다!";
			mes "" + strcharinfo(0) + ", 이것은 승리의 징표요!";
			close2;
			set .@Reward, 10;
			set .@Reward, 10;
		}
		else
		{ // 
			mes "[이십인장]";
			mes "당신은 패배하였지만, 이 전투에 참가하였소.";
			mes "이것은 기욤 마롤로를 도와 전투에 참가한 것에 대한 보상이오!";
			mes "이 패배를 교훈으로 삼아, 당신은 다음 번에는 반드시 승리할 것이오!";
			close2;
			set .@Reward, 17;
			set .@Reward, 7;
		}

		set Flavius_BG_Tick, gettimetick(2) + 150;
		getitem 7829, .@Reward;
		getitem 671, .@Reward2;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}

bat_b02,10,293,5	script	크루아 이십인장#fl2	415,{
	if( $@FlaviusBG2_Victory )
	{
		if( $@FlaviusBG2_Victory == Bat_Team )
		{ // Victory
			mes "[이십인장]";
			mes "크루아에게 축복을!!";
			mes "우리의 영광스러운 승리를 즐깁시다!";
			mes "" + strcharinfo(0) + ", 이것은 승리의 징표요!";
			close2;
			set .@Reward, 10;
			set .@Reward, 10;
		}
		else
		{ // 
			mes "[이십인장]";
			mes "오, " + strcharinfo(0) + ". 슬퍼하지 말게나.";
			mes "우리가 비록 이기지 못했지만, 우리는 최선을 다했네.";
			mes "이것은 크루아 님으로부터의 선물이네. 그리고, 이 전투를 잊지 말게나. 우리는 다음 번에는 승리할 것일세.";
			close2;
			set .@Reward, 7;
			set .@Reward, 7;
		}

		set Flavius_BG_Tick, gettimetick(2) + 150;
		getitem 7829, .@Reward;
		getitem 671, .@Reward2;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}

// Battleground Therapist
// *********************************************************************

bat_b02,390,13,5	script	전장의 치료사#fl22	95,{
	mes "[전장의 치료사]";
	mes "잠시 눈을 감고, 깊게 숨을 들이쉬세요.";
	mes "당신은 고통으로부터 자유로워져야 합니다.";
	specialeffect2 312; percentheal 100,100;
	specialeffect2 EF_INCAGILITY; sc_start SC_INCREASEAGI,240000,10;
	specialeffect2 EF_BLESSING; sc_start SC_BLESSING,240000,10;
	close;
}

bat_b02,10,293,5	script	전장의 치료사#fl21	95,{
	mes "[전장의 치료사]";
	mes "잠시 눈을 감고, 깊게 숨을 들이쉬세요.";
	mes "당신은 고통으로부터 자유로워져야 합니다.";
	specialeffect2 312; percentheal 100,100;
	specialeffect2 EF_INCAGILITY; sc_start SC_INCREASEAGI,240000,10;
	specialeffect2 EF_BLESSING; sc_start SC_BLESSING,240000,10;
	close;
}

// Battleground Respawn
// *********************************************************************

bat_b02,390,10,0	script	#gfl2_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b02",382,2,397,17,100,100;
	areawarp "bat_b02",382,2,397,17,"bat_b02",311,224;
	initnpctimer;
	end;
}

bat_b02,10,290,0	script	#cfl2_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_b02",2,282,17,297,100,100;
	areawarp "bat_b02",2,282,17,297,"bat_b02",87,75;
	initnpctimer;
	end;
}

// Flags
// *********************************************************************

bat_b02,304,231,1	duplicate(기욤 진영#bat)	기욤 진영#bat32	973
bat_b02,319,231,1	duplicate(기욤 진영#bat)	기욤 진영#bat33	973
bat_b02,304,218,1	duplicate(기욤 진영#bat)	기욤 진영#bat34	973
bat_b02,319,218,1	duplicate(기욤 진영#bat)	기욤 진영#bat35	973
bat_b02,304,231,1	duplicate(기욤 진영#bat)	기욤 진영#bat36	973
bat_b02,304,231,1	duplicate(기욤 진영#bat)	기욤 진영#bat37	973
bat_b02,335,142,1	duplicate(기욤 진영#bat)	기욤 진영#bat38	973
bat_b02,335,157,1	duplicate(기욤 진영#bat)	기욤 진영#bat39	973
bat_b02,390,16,1	duplicate(기욤 진영#bat)	기욤 진영#bat40	973
bat_b02,292,163,1	duplicate(기욤 진영#bat)	기욤 진영#bat41	973
bat_b02,292,136,1	duplicate(기욤 진영#bat)	기욤 진영#bat42	973
bat_b02,241,185,1	duplicate(기욤 진영#bat)	기욤 진영#bat43	973
bat_b02,247,179,1	duplicate(기욤 진영#bat)	기욤 진영#bat44	973

bat_b02,96,81,1	duplicate(크루아 진영#bat)	크루아 진영#bat30	974
bat_b02,96,68,1	duplicate(크루아 진영#bat)	크루아 진영#bat31	974
bat_b02,79,81,1	duplicate(크루아 진영#bat)	크루아 진영#bat32	974
bat_b02,79,68,1	duplicate(크루아 진영#bat)	크루아 진영#bat33	974
bat_b02,96,81,1	duplicate(크루아 진영#bat)	크루아 진영#bat34	974
bat_b02,96,81,1	duplicate(크루아 진영#bat)	크루아 진영#bat35	974
bat_b02,59,164,1	duplicate(크루아 진영#bat)	크루아 진영#bat36	974
bat_b02,59,137,1	duplicate(크루아 진영#bat)	크루아 진영#bat37	974
bat_b02,10,296,1	duplicate(크루아 진영#bat)	크루아 진영#bat38	974
bat_b02,110,162,1	duplicate(크루아 진영#bat)	크루아 진영#bat39	974
bat_b02,110,137,1	duplicate(크루아 진영#bat)	크루아 진영#bat40	974
bat_b02,152,120,1	duplicate(크루아 진영#bat)	크루아 진영#bat41	974
bat_b02,158,114,1	duplicate(크루아 진영#bat)	크루아 진영#bat42	974

// MapFlags
// *********************************************************************

bat_b02	mapflag	battleground
bat_b02	mapflag	nomemo
bat_b02	mapflag	nosave	SavePoint
bat_b02	mapflag	noteleport
bat_b02	mapflag	nowarp
bat_b02	mapflag	nowarpto
bat_b02	mapflag	noreturn
bat_b02	mapflag	nobranch
bat_b02	mapflag	nopenalty
