// ==============================================================================
// BattleGround System - Tierra Valley 1
// ==============================================================================

// Registration NPC's
// *********************************************************************

bat_room,85,227,4	script	Registration::TV1R_Guillaume	418,{
	end;
OnInit:
	delwaitingroom;
	waitingroom "전투 대기실 - "+$Tier+" 명",$Tier+1,"Tierra_BG1::OnGuillaumeJoin",1;
	end;
OnEnterBG:
	set $@TierraBG1_id1, waitingroom2bg("bat_a01",50,374,"Tierra_BG1::OnGuillaumeQuit","");
	end;
OnTieNoin:
	delwaitingroom;
	waitingroom "전투 대기실 - 인원 조정중",0;
	end;
OnTieWait:
	delwaitingroom;
	waitingroom "전투 대기실 - "+$Tier+" 명",$Tier+1,"Tierra_BG1::OnGuillaumeJoin",1;
	end;
OnStartBG:
	delwaitingroom;
	waitingroom "전투 대기실 - 전투 중...",0;
	end;
}

bat_room,85,204,0	script	Registration::TV1R_Croix	414,{
	end;
OnInit:
	delwaitingroom;
	waitingroom "전투 대기실 - "+$Tier+" 명",$Tier+1,"Tierra_BG1::OnCroixJoin",1;
	end;
OnEnterBG:
	set $@TierraBG1_id2, waitingroom2bg("bat_a01",42,16,"Tierra_BG1::OnCroixQuit","");
	end;
OnTieNoin:
	delwaitingroom;
	waitingroom "전투 대기실 - 인원 조정중",0;
	end;
OnTieWait:
	delwaitingroom;
	waitingroom "전투 대기실 - "+$Tier+" 명",$Tier+1,"Tierra_BG1::OnCroixJoin",1;
	end;
OnStartBG:
	delwaitingroom;
	waitingroom "전투 대기실 - 전투 중...",0;
	end;
}

// Battleground Engine
// *********************************************************************

-	script	Tierra_BG1	-1,{
	end;

OnInit:
	disablenpc "크루아 이십인장#tv1";
	disablenpc "기욤 이십인장#tv1";
	disablenpc "전장의 치료사#tv11";
	disablenpc "전장의 치료사#tv12";
	end;

OnGuillaumeQuit:
OnCroixQuit:
	set BG_Delay_Tick, gettimetick(2) + 600;
	end;

OnGuillaumeJoin:
OnCroixJoin:
	if( $@TierraBG1 == 0 )
		donpcevent "Tierra_BG1::OnReadyCheck";
	end;

OnReadyCheck:
	if( $@TierraBG1 )
		end;
	set .@Guillaume, getwaitingroomstate(0,"TV1R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"TV1R_Croix");

	if( .@Guillaume < $Tier || .@Croix < $Tier || .@Guillaume != .@Croix )
	{
		mapannounce "bat_room","전장 - 티에라 협곡 전투 기욤 진영 : 현재" + .@Guillaume + "/"+$Tier+"명, 크루아 진영 : 현재" + .@Croix + "/"+$Tier+"명",1,0xFFA500;
		end;
	}

	sleep 10000;
	
	set .@Guillaume_R, getwaitingroomstate(0,"TV1R_Guillaume");
	set .@Croix_R, getwaitingroomstate(0,"TV1R_Croix");

	if( .@Guillaume_R != .@Croix_R ) 
	{ 
		donpcevent "Tierra_BG1::OnReadyCheck"; 
		end; 
	} 
	
	set $@TierraBG1, 1;
	donpcevent "TV1R_Croix::OnEnterBG";
	donpcevent "TV1R_Guillaume::OnEnterBG";
	donpcevent "Guillaume_TV1B::OnBuild";
	donpcevent "Croix_TV1B::OnBuild";
	bg_monster $@TierraBG1_id1,"bat_a01",176,345,"기욤 군 식량 창고",1909,"Tierra_BG1::OnGuillaumeBreak";
	bg_monster $@TierraBG1_id2,"bat_a01",167,50,"크루아 군 식량 창고",1910,"Tierra_BG1::OnCroixBreak";
	bg_monster 0,"bat_a01",273,204,"중립 깃발",1911,"Tierra_BG1::OnNeutralBreak";
	donpcevent "TV2R_Croix::OnStartBG";
	donpcevent "TV2R_Guillaume::OnStartBG";
	
	set .Neutral_Base, 0;
	set $@TierraBG1_Victory, 0;

	set .Guardian_1, 0;
	set .Guardian_2, 0;
	set .Guardian_3, 0;

	enablenpc "전장의 치료사#tv11";
	enablenpc "전장의 치료사#tv12";
	disablenpc "크루아 이십인장#tv1";
	disablenpc "기욤 이십인장#tv1";
	// Respawner
	donpcevent "#gtv1_respawn::OnBGStart";
	donpcevent "#ctv1_respawn::OnBGStart";
	donpcevent "#ntv1_respawn::OnBGStart";
	// Warp Teams
	announce "전장 - 티에라 협곡 전투가 시작되었습니다!",0,0xFFA500;
	bg_warp $@TierraBG1_id1,"bat_a01",353,344;
	bg_warp $@TierraBG1_id2,"bat_a01",353,52;
	// Final Messages
	sleep 6000;
	mapannounce "bat_a01","기욤 이십인장 : 크루아 군의 식량 창고를 불태워 버리자!",1,0x0000FF;
	sleep 2000;
	mapannounce "bat_a01","크루아 이십인장 : 발할라의 여신이여! 우리에게 믿음과 용기를 주소서!",1,0xFF0000;
	
	initnpctimer;
	end;

OnNeutralBreak:
	if( getcharid(4) == $@TierraBG1_id1 )
	{
		mapannounce "bat_a01","기욤 진영이 중립 깃발을 점령하였습니다.",1,0x0000FF;
		bg_team_setxy $@TierraBG1_id2,42,16;
		areawarp "bat_a01",52,208,61,217,"bat_a01",42,16;
	}
	else if( getcharid(4) == $@TierraBG1_id2 )
	{
		mapannounce "bat_a01","크루아 진영이 중립 깃발을 점령하였습니다.",1,0xFF0000;
		bg_team_setxy $@TierraBG1_id1,50,374;
		areawarp "bat_a01",52,208,61,217,"bat_a01",50,374;
	}
	else end;

	set .Neutral_Base, getcharid(4);
	bg_team_setxy .Neutral_Base,56,212;

	if( .Guardian_1 == 0 )
		set .Guardian_1, bg_monster(.Neutral_Base,"bat_a01",280,233,"가디언",1949,"Tierra_BG1::OnGuardian1");
	else
		bg_monster_set_team .Guardian_1, .Neutral_Base;
	
	if( .Guardian_2 == 0 )
		set .Guardian_2, bg_monster(.Neutral_Base,"bat_a01",287,203,"가디언",1950,"Tierra_BG1::OnGuardian2");
	else
		bg_monster_set_team .Guardian_2, .Neutral_Base;

	if( .Guardian_3 == 0 )
		set .Guardian_3, bg_monster(.Neutral_Base,"bat_a01",268,204,"가디언",1949,"Tierra_BG1::OnGuardian3");
	else
		bg_monster_set_team .Guardian_3, .Neutral_Base;

	sleep 10000;
	if( $@TierraBG1 != 1 ) end;

	if( .Neutral_Base == $@TierraBG1_id1 )
		bg_monster .Neutral_Base,"bat_a01",273,204,"기욤 진영 깃발",1912,"Tierra_BG1::OnNeutralBreak";
	else if( .Neutral_Base == $@TierraBG1_id2 )
		bg_monster .Neutral_Base,"bat_a01",273,204,"크루아 진영 깃발",1913,"Tierra_BG1::OnNeutralBreak";
	end;

OnGuardian1: set .Guardian_1, 0; end;
OnGuardian2: set .Guardian_2, 0; end;
OnGuardian3: set .Guardian_3, 0; end;

OnGuillaumeBreak:
	set $@TierraBG1_Victory, 2;
	mapannounce "bat_a01","크루아 이십인장 : 기욤 군의 식량 창고를 없앴어! 우리의 승리입니다!",1,0xFF0000;
	donpcevent "Tierra_BG1::OnStop";
	end;

OnCroixBreak:
	set $@TierraBG1_Victory, 1;
	mapannounce "bat_a01","기욤 이십인장 : 크루아 군의 식량 창고를 없앴어! 우리의 승리입니다!",1,0x0000FF;
	donpcevent "Tierra_BG1::OnStop";
	end;

OnTimer2400000:
	mapannounce "bat_a01","티에라 협곡 전투가 5분 후에 종료됩니다.",1,0xFFA500;
	end;
OnTimer2640000:
	mapannounce "bat_a01","티에라 협곡 전투가 1분 후에 종료됩니다.",1,0xFFA500;
	end;

OnTimer2700000:
	set $@TierraBG1_Victory, 3; // Draw Game
	mapannounce "bat_a01","티에라 협곡 전투가 무승부로 끝났습니다.",1,0xFFA500;
	donpcevent "Tierra_BG1::OnStop";
	end;

OnStop:
	set $@TierraBG1, 2;
	disablenpc "전장의 치료사#tv11";
	disablenpc "전장의 치료사#tv12";
	donpcevent "#gtv1_respawn::OnBGStop";
	donpcevent "#ctv1_respawn::OnBGStop";
	donpcevent "#ntv1_respawn::OnBGStop";
	enablenpc "크루아 이십인장#tv1";
	enablenpc "기욤 이십인장#tv1";
	killmonster "bat_a01","Tierra_BG1::OnGuillaumeBreak";
	killmonster "bat_a01","Tierra_BG1::OnCroixBreak";
	killmonster "bat_a01","Tierra_BG1::OnNeutralBreak";
	killmonster "bat_a01","Tierra_BG1::OnGuardian1";
	killmonster "bat_a01","Tierra_BG1::OnGuardian2";
	killmonster "bat_a01","Tierra_BG1::OnGuardian3";
	stopnpctimer;
	sleep 3000;
	bg_warp $@TierraBG1_id1,"bat_a01",50,374;
	bg_warp $@TierraBG1_id2,"bat_a01",42,16;
	sleep 3000;
	mapannounce "bat_a01","티에라 협곡 전투가 1분 후에 닫힙니다!",1,0xFFA500;
	initnpctimer;
	end;

OnTimer30000:
	if( $@TierraBG1 == 2 )
		mapannounce "bat_a01","티에라 협곡 전투가 30초 후에 닫힙니다!",1,0xFFA500;
	end;
OnTimer50000:
	if( $@TierraBG1 == 2 )
		mapannounce "bat_a01","티에라 협곡 전투가 10초 후에 닫힙니다!",1,0xFFA500;
	end;

OnTimer60000:
	if( $@TierraBG1 != 2 )
		end;
OnReset:
	stopnpctimer;
	set .Neutral_Base, 0;
	disablenpc "크루아 이십인장#tv1";
	disablenpc "기욤 이십인장#tv1";
	disablenpc "전장의 치료사#tv11";
	disablenpc "전장의 치료사#tv12";
	donpcevent "#gtv1_respawn::OnBGStop";
	donpcevent "#ctv1_respawn::OnBGStop";
	donpcevent "#ntv1_respawn::OnBGStop";

	killmonster "bat_a01","Tierra_BG1::OnGuillaumeBreak";
	killmonster "bat_a01","Tierra_BG1::OnCroixBreak";
	killmonster "bat_a01","Tierra_BG1::OnNeutralBreak";
	killmonster "bat_a01","Tierra_BG1::OnGuardian1";
	killmonster "bat_a01","Tierra_BG1::OnGuardian2";
	killmonster "bat_a01","Tierra_BG1::OnGuardian3";
	donpcevent "Guillaume_TV1B::OnDestroy";
	donpcevent "Croix_TV1B::OnDestroy";
	donpcevent "TV2R_Croix::OnInit";
	donpcevent "TV2R_Guillaume::OnInit";

	set $@TierraBG1_Victory, 0;
	if( $@TierraBG1_id1 ) { bg_destroy $@TierraBG1_id1; set $@TierraBG1_id1, 0; }
	if( $@TierraBG1_id2 ) { bg_destroy $@TierraBG1_id2; set $@TierraBG1_id2, 0; }
	sleep 1000;
	mapwarp "bat_a01","bat_room",155,150;
	sleep 2000;
	maprespawnguildid "bat_a01",0,3; // Just in case someone else
	sleep 2000;
	set $@TierraBG1, 0;
	donpcevent "Tierra_BG1::OnReadyCheck"; // Maybe a game is ready to start
	end;
}

// MapFlags
// *********************************************************************

bat_a01	mapflag	battleground
bat_a01	mapflag	nomemo
bat_a01	mapflag	nosave	SavePoint
bat_a01	mapflag	noteleport
bat_a01	mapflag	nowarp
bat_a01	mapflag	nowarpto
bat_a01	mapflag	noreturn
bat_a01	mapflag	nobranch
bat_a01	mapflag	nopenalty

// Other Flags
// *********************************************************************

bat_a01,148,85,1	duplicate(크루아 진영#bat)	크루아 진영#bat10	974
bat_a01,155,85,1	duplicate(크루아 진영#bat)	크루아 진영#bat11	974
bat_a01,357,75,1	duplicate(크루아 진영#bat)	크루아 진영#bat12	974
bat_a01,348,74,1	duplicate(크루아 진영#bat)	크루아 진영#bat13	974
bat_a01,199,49,1	duplicate(크루아 진영#bat)	크루아 진영#bat14	974
bat_a01,168,16,1	duplicate(크루아 진영#bat)	크루아 진영#bat15	974
bat_a01,138,12,1	duplicate(크루아 진영#bat)	크루아 진영#bat16	974
bat_a01,108,35,1	duplicate(크루아 진영#bat)	크루아 진영#bat17	974
bat_a01,164,308,1	duplicate(기욤 진영#bat)	기욤 진영#bat10	973
bat_a01,157,308,1	duplicate(기욤 진영#bat)	기욤 진영#bat11	973
bat_a01,359,327,1	duplicate(기욤 진영#bat)	기욤 진영#bat12	973
bat_a01,350,326,1	duplicate(기욤 진영#bat)	기욤 진영#bat13	973
bat_a01,209,344,1	duplicate(기욤 진영#bat)	기욤 진영#bat14	973
bat_a01,173,380,1	duplicate(기욤 진영#bat)	기욤 진영#bat15	973
bat_a01,150,380,1	duplicate(기욤 진영#bat)	기욤 진영#bat16	973
bat_a01,118,357,1	duplicate(기욤 진영#bat)	기욤 진영#bat17	973
bat_a01,119,336,1	duplicate(기욤 진영#bat)	기욤 진영#bat18	973

// Barricades
// *********************************************************************

-	script	Guillaume_TV1B	-1,{
	end;

OnBuild:
	for( set .@i, 0; .@i < 16; set .@i, .@i + 1 )
		bg_monster 0,"bat_a01",170+.@i,130,"바리케이드",1906,"Guillaume_TV1B::OnWall";

	setwall "bat_a01",170,130,16,6,1,"bat_a01_g1";
	set .MyMobCount,16;
	end;

OnDestroy:
	killmonster "bat_a01","Guillaume_TV1B::OnWall";
	delwall "bat_a01_g1";
	set .MyMobCount,0;
	end;

OnWall:
	if( set(.MyMobCount,.MyMobCount - 1) < 1 )
	{
		delwall "bat_a01_g1";
		mapannounce "bat_a01","남문 : 게이트가 파괴되었습니다!!",1,0xFFA500;
	}
	end;
}

-	script	Croix_TV1B	-1,{
	end;

OnBuild:
	for( set .@i, 0; .@i < 16; set .@i, .@i + 1 )
		bg_monster 0,"bat_a01",186+.@i,266,"바리케이드",1906,"Croix_TV1B::OnWall";
	
	setwall "bat_a01",186,266,16,6,1,"bat_a01_c1";
	set .MyMobCount,16;
	end;

OnDestroy:
	killmonster "bat_a01","Croix_TV1B::OnWall";
	delwall "bat_a01_c1";
	set .MyMobCount,0;
	end;

OnWall:
	if( set(.MyMobCount,.MyMobCount - 1) < 1 )
	{
		delwall "bat_a01_c1";
		mapannounce "bat_a01","북문 : 게이트가 파괴되었습니다!!",1,0xFFA500;
	}
	end;
}

// Battleground rewards
// *********************************************************************

bat_a01,45,19,3	script	크루아 이십인장#tv1	415,{
	if( $@TierraBG1_Victory )
	{
		if( $@TierraBG1_Victory == Bat_Team )
		{ // Victory
			set .@Reward, 5;
			set .@Reward2, 10;
			mes "[이십인장]";
			mes "크루아에게 축복을!!";
			mes "우리의 영광스러운 승리를 즐깁시다!";
			mes "" + strcharinfo(0) + ", 이것은 승리의 징표요!";
			close2;
		}
		else
		{ // 
			set .@Reward, 3;
			set .@Reward2, 7;
			mes "[이십인장]";
			mes "오, " + strcharinfo(0) + ". 슬퍼하지 말게나.";
			mes "우리가 비록 이기지 못했지만, 우리는 최선을 다했네.";
			mes "이것은 크루아 님으로부터의 선물이네. 그리고, 이 전투를 잊지 말게나. 우리는 다음 번에는 승리할 것일세.";
			close2;
		}

		set Tierra_BG_Tick, gettimetick(2) + 150;
		getitem 7828, .@Reward;
		getitem 671, .@Reward2;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}

bat_a01,53,377,3	script	기욤 이십인장#tv1	419,{
	if( $@TierraBG1_Victory )
	{
		if( $@TierraBG1_Victory == Bat_Team )
		{ // Victory
			set .@Reward, 5;
			set .@Reward2, 10;
			mes "[이십인장]";
			mes "기욤에게 축복을!!";
			mes "우리의 영광스러운 승리를 즐깁시다!";
			mes "" + strcharinfo(0) + ", 이것은 승리의 징표요!";
			close2;
		}
		else
		{ // 
			set .@Reward, 3;
			set .@Reward2, 7;
			mes "[이십인장]";
			mes "당신은 패배하였지만, 이 전투에 참가하였소.";
			mes "이것은 기욤 마롤로를 도와 전투에 참가한 것에 대한 보상이오!";
			mes "이 패배를 교훈으로 삼아, 당신은 다음 번에는 반드시 승리할 것이오!";
			close2;
		}

		set Tierra_BG_Tick, gettimetick(2) + 150;
		getitem 7828, .@Reward;
		getitem 671, .@Reward2;
		bg_leave;
		warp "bat_room",155,150;
		end;
	}
	end;
}

// Battleground Therapist
// *********************************************************************

bat_a01,60,216,3	script	유령#tv13	950,{
	mes "[협곡의 유령]";
	mes "우...우...";
	specialeffect2 312;
	close;
}

bat_a01,53,377,3	script	전장의 치료사#tv12	95,{
	mes "[전장의 치료사]";
	mes "잠시 눈을 감고, 깊게 숨을 들이쉬세요.";
	mes "당신은 고통으로부터 자유로워져야 합니다.";
	specialeffect2 312; percentheal 100,100;
	specialeffect2 EF_INCAGILITY; sc_start SC_INCREASEAGI,240000,10;
	specialeffect2 EF_BLESSING; sc_start SC_BLESSING,240000,10;
	close;
}

bat_a01,45,18,3	script	전장의 치료사#tv11	95,{
	mes "[전장의 치료사]";
	mes "잠시 눈을 감고, 깊게 숨을 들이쉬세요.";
	mes "당신은 고통으로부터 자유로워져야 합니다.";
	specialeffect2 312; percentheal 100,100;
	specialeffect2 EF_INCAGILITY; sc_start SC_INCREASEAGI,240000,10;
	specialeffect2 EF_BLESSING; sc_start SC_BLESSING,240000,10;
	close;
}

// Battleground Respawn
// *********************************************************************

bat_a01,57,213,0	script	#ntv1_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer19000:
	misceffect 83;
	end;

OnTimer20000:
	areapercentheal "bat_a01",52,208,61,217,100,100;
	areawarp "bat_a01",52,208,61,217,"bat_a01",301,208;
	initnpctimer;
	end;
}

bat_a01,50,374,0	script	#gtv1_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_a01",46,370,54,378,100,100;
	areawarp "bat_a01",46,370,54,378,"bat_a01",354,340;
	initnpctimer;
	end;
}

bat_a01,42,16,0	script	#ctv1_respawn	139,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	misceffect 83;
	end;

OnTimer25000:
	areapercentheal "bat_a01",38,12,47,21,100,100;
	areawarp "bat_a01",38,12,47,21,"bat_a01",354,57;
	initnpctimer;
	end;
}
