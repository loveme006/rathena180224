//===== rAthena Script ======================================= 
//= Warper
//===== By: ================================================== 
//= Euphy
//===== Current Version: =====================================
//= 1.4a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: ========================================= 
//= A complete - but very condensed - warper script.
//= Some coordinates written by Tekno-Kanix and ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.1 Added missing duplicates and fixed coordinates.
//= 1.2 Added new episodes and simplified functions.
//= 1.3 Added Renewal checks and Instances menu.
//=     Aligned coordinates with @go.
//= 1.4 Added new Guild Dungeons.
//= 1.4a Slight edits.
//============================================================

-	script	Warper	-1,{
function Go; function Disp; function Pick; function Restrict; function partywarp;

// --------------------------------------------------
//	Main:
// --------------------------------------------------

menu	"마지막 위치 ^777777["+lastwarp$+"]^000000",-,
	" ~ 추천 사냥터", Recommand,
    	" ~ 도시",Towns,
    	" ~ 필드",Fields,
    	" ~ 던전",Dungeons,
    	//" ~ 길드 성",Castles,
    	" ~ 길드 던전",Guild_Dungeons,
    	" ~ 특별지역",Special,
    	" ~ 인던",Instances;

	if (lastwarp$ == "")
		message strcharinfo(0),"아직 아무데도 가본적이 없소.";
	else 
		warp lastwarp$,lastwarpx,lastwarpy;
	end;

// ------------------- Functions -------------------
// * Go("<map>",<x>,<y>);
//	~ Warps directly to a map.
//
// * Disp("<Menu Option>",<first option>,<last option>);
// * Pick("<map_prefix>"{,<index offset>});
//	~ Dynamic menu and map selection (auto-numbered).
//
// * Disp("<Option 1>:<Option 2>:<etc.>");
// * Pick("","<map1>","<map2>","<etc.>");
//	~ Manual menu and map selection (listed).
//
// * Restrict("<RE | Pre-RE>"{,<menu option numbers>});
//	~ Only allows map for Renewal or Pre-Renewal modes.
//     If menu option numbers are given, only those maps
//     will be restricted (i.e. not for "Go").
//
// Other notes:
//   ~ Array @c[] holds all (x,y) coordinates.
//   ~ Use @c[2] EXCEPT when maps begin dynamically
//	  at 0: use @c[0] and Pick() offset 1.
// --------------------------------------------------

function Go {
	if(getcharid(0) == getpartyleader(getcharid(1),2)) {
	mes "파티로 이동하시겠소?";
	menu "엔터방지", Enter, "네", pY, "아니요", -;
	}
	set lastwarp$, getarg(0);
	set lastwarpx, getarg(1,0);
	set lastwarpy, getarg(2,0);
	warp getarg(0),getarg(1,0),getarg(2,0);
	end;
pY:
	partywarp(getarg(0),getarg(1,0),getarg(2,0));
Enter:
	Go(getarg(0),getarg(1,0),getarg(2,0));
}

function partywarp {
    if(!getcharid(3)) end;
    set .@party_id, getcharid(1);
    set .@map$, getarg(0);
    set .@x, getarg(1);
    set .@y, getarg(2);
    set lastwarp$, getarg(0);
    set lastwarpx, getarg(1,0);
    set lastwarpy, getarg(2,0);
    set @party, "";
    warpparty .@map$,.@x,.@y,.@party_id;
    end;
}
 


function Disp {
	if (getargcount() < 3)
		set @menu$, getarg(0);
	else {
		set @menu$,"";
		for (set .@i,getarg(1); .@i<=getarg(2); set .@i,.@i+1)
			set @menu$, @menu$+getarg(0)+" "+.@i+":";
	}
	return;
}
function Pick {
	set .@warp_block,@warp_block;
	set @warp_block,0;
	set .@select, select(@menu$);
	if (getarg(0) == "") {
		set .@i, .@select;
		set .@map$, getarg(.@i);
	} else {
		set .@i, .@select-getarg(1,0);
		set .@map$, getarg(0)+((.@i<10)?"0":"")+.@i;
	}
	if (.@warp_block & (1<<.@select)) {
		message strcharinfo(0),"This map is not enabled in "+((checkre(0))?"":"Pre-")+"Renewal.";
		end;
	}
	set .@x, @c[.@i*2];
	set .@y, @c[.@i*2+1];
	deletearray @c[0],getarraysize(@c);
	Go(.@map$,.@x,.@y);
}
function Restrict {
	if ((getarg(0) == "RE" && !checkre(0)) || (getarg(0) == "Pre-RE" && checkre(0))) {
		if (getarg(1,0)) {
			set @warp_block,0;
			for (set .@i,1; .@i<getargcount(); set .@i,.@i+1)
				set @warp_block, @warp_block | (1<<getarg(.@i));
		} else {
			message strcharinfo(0),"This map is not enabled in "+((checkre(0))?"":"Pre-")+"Renewal.";
			end;
		}
	}
	return;
}
// --------------------------------------------------
	Recommand:
// --------------------------------------------------
menu	"페이욘던전 1층[10-32]", R1,
	"오크 마을[29-59]",R2,
	"오크 던전[36-66]",R3,
	"게펜 필드[64-84]",R4,
	"시계탑 지하4층[71-101]",R5,
	"글레스트헤임 지하수로4[83-113]",R6,
	"글레스트헤임 지하묘지[84-114]",R7,
	"베인스 필드[95-125]",R8,
	"노그로드 2층[100-120]",R9,
	"유페로스 1층[100-128]",R10,
	"라헬 신전 1층[108-138]",R11,
	"스카라바홀 2층[118-145]",R12,
	"에클라쥬 필드 1[128-158]",R13,
	"비프로스트 필드 1[129-155]",R14,
	"시계탑 나이트메어 3[150-174]",R15;

R1: Go("pay_dun00",21,179);
R2: Go("gef_fild10",240,198);
R3: Go("orcsdun01",32,168);
R4: Go("gef_fild06",248,158);
R5: Go("alde_dun04",204,221);
R6: Go("gl_sew04",67,274);
R7: Go("gl_chyard",147,15);
R8: Go("ve_fild03",222,45);
R9: Go("mag_dun02",47,30);
R10: Go("juperos_01",53,247);
R11: Go("ra_san01",139,18);
R12: Go("dic_dun02",102,143);
R13: Go("ecl_fild01",100,317);
R14: Go("bif_fild01",193,220);
R15: Go("c_tower3_",46,42);
// --------------------------------------------------
	Towns:
// --------------------------------------------------
menu	"게펜",T13, "곤륜",T14, "니플헤임",T29, "데와타",T8,"라자냐",T37,"라헬",T31, "루티에",T20, "뤄양",T19, 
	"리히타르젠",T18, "마누크",T23, "말랑도",T21, "모라",T25, "모로크",T26, "모스코비아",T27,
	"무명섬",T28, "미드가르드 연합 주둔지",T24, "베인스",T35, "브라질리아",T6,"스플랑디드",T32, 
	"아마쯔",T4, "아요타야",T5, "아인베흐",T10, "아인브로크",T11, "알데바란",T3, "알베르타",T2,
	"에클라쥬",T9, "엘 디카스네스",T12, "이즈루드",T16, "자와이",T17, "움발라",T34, "유노",T36 ,
	"코모도",T7, "토르 화산 병참기지",T33,"페이욘",T30,"포트 말라야",T22, "프론테라",T1, "휘겔",T15;

T1: Go("prontera",155,183);
T2: Go("alberta",28,234);
T3: Go("aldebaran",140,131);
T4: Go("amatsu",198,84);
T5: Go("ayothaya",208,166);
T6: Restrict("RE");
	Go("brasilis",196,217);
T7: Go("comodo",209,143);
T8: Restrict("RE");
	Go("dewata",200,180);
T9: Restrict("RE");
	Go("ecl_in01",48,53);
T10: Go("einbech",63,35);
T11: Go("einbroch",64,200);
T12: Restrict("RE");
	 Go("dicastes01",198,187);
T13: Go("geffen",119,59);
T14: Go("gonryun",160,120);
T15: Go("hugel",96,145);
T16: Go("izlude",128,(checkre(3)?146:114));
T17: Go("jawaii",251,132);
T18: Go("lighthalzen",158,92);
T19: Go("louyang",217,100);
T20: Go("xmas",147,134);
T21: Restrict("RE");
	 Go("malangdo",140,114);
T22: Restrict("RE");
	 Go("malaya",231,200);
T23: Go("manuk",282,138);
T24: Go("mid_camp",210,288);
T25: Restrict("RE");
	 Go("mora",55,146);
T26: Go("morocc",156,93);
T27: Go("moscovia",223,184);
T28: Go("nameless_n",256,215);
T29: Go("niflheim",202,174);
T30: Go("payon",179,100);
T31: Go("rachel",130,110);
T32: Go("splendide",201,147);
T33: Go("thor_camp",246,68);
T34: Go("umbala",97,153);
T35: Go("veins",216,123);
T36: Go("yuno",157,51);
T37: Restrict("RE");
	 Go("lasagna",151,155);

// --------------------------------------------------
	Fields:
// --------------------------------------------------
menu	"게펜 필드",F10, "곤륜 필드",F11, "니플헤임 필드",F20,"데와타필드",F6,"라자냐필드",F29,
	"라헬필드",F23, "루티에 필드",F15, "뤄양 필드",F14, "리히타르젠 필드",F13,
	"마누크 필드",F17, "말라야 필드",F16, "모스코비아 필드",F19, "묘르닐 산맥",F18,
	"베인스필드",F27, "브라질리아 필드",F4, "비프로스트 필드", F3, "소그라트사막",F24, 
	"스플랑디드필드",F25, "아마쯔 필드",F1, "아요타야 필드",F2, "아인브로크 필드",F8,
	"움발라필드",F26, "유노필드",F28, "에클라쥬 필드",F7, "엘 디카스네스 필드",F9,
    	"코모도 필드",F5, "페이욘숲",F21, "프론테라필드",F22, "휘겔 필드",F12;

F1: setarray @c[2],190,197;
	Disp("아마쯔 필드",1,1); Pick("ama_fild");
F2: setarray @c[2],173,134,212,150;
	Disp("아요타야필드",1,2); Pick("ayo_fild");
F3: Restrict("RE");
	setarray @c[2],193,220,220,187;
	Disp("비프로스트필드",1,2); Pick("bif_fild");
F4: Restrict("RE");
	setarray @c[2],74,32;
	Disp("브라질리아필드",1,1); Pick("bra_fild");
F5: Restrict("Pre-RE",5);
	setarray @c[2],180,178,231,160,191,172,228,194,224,203,190,223,234,177,194,175,172,172;
	Disp("코모도 필드",1,9); Pick("cmd_fild");
F6: Restrict("RE");
	setarray @c[2],371,212;
	Disp("데와타 필드",1,1); Pick("dew_fild");
F7: Restrict("RE");
	setarray @c[2],97,314;
	Disp("엘클라쥬필드",1,1); Pick("ecl_fild");
F8: Restrict("Pre-RE",2,10);
	setarray @c[2],142,225,182,141,187,228,185,173,216,173,195,148,272,220,173,214,207,174,196,200;
	Disp("아인브로크필드",1,10); Pick("ein_fild");
F9: Restrict("RE");
	setarray @c[2],143,132,143,217;
	Disp("엘디카스네스필드",1,2); Pick("dic_fild");
F10: Restrict("Pre-RE",13,15);
	 setarray @c[0],46,199,213,204,195,212,257,192,188,171,166,263,248,158,195,191,186,183,221,117,178,218,136,328,240,181,235,235,211,185;
	 Disp("게펜필드",0,14); Pick("gef_fild",1);
F11: setarray @c[2],220,227;
	 Disp("곤륜필드",1,1); Pick("gon_fild");
F12: Restrict("Pre-RE",3,7);
	 setarray @c[2],268,101,222,193,232,185,252,189,196,106,216,220,227,197;
	 Disp("휘겔필드",1,7); Pick("hu_fild");
F13: setarray @c[2],240,179,185,235,240,226;
	 Disp("리히타르젠필드",1,3); Pick("lhz_fild");
F14: setarray @c[2],229,187;
	 Disp("뤄양필드",1,1); Pick("lou_fild");
F15: setarray @c[2],115,145;
	 Disp("루티에필드",1,1); Pick("xmas_fild");
F16: Restrict("RE");
	 setarray @c[2],40,272,207,180;
	 Disp("말라야필드",1,2); Pick("ma_fild");
F17: setarray @c[2],35,236,35,262,84,365;
	 Disp("마누크필드",1,3); Pick("man_fild");
F18: setarray @c[2],204,120,175,193,208,213,179,180,181,240,195,270,235,202,188,215,205,144,245,223,180,206,196,208;
	 Disp("묘르닐필드",1,12); Pick("mjolnir_");
F19: setarray @c[2],82,104,131,147;
	 Disp("모스코비아필드",1,2); Pick("mosk_fild");
F20: setarray @c[2],215,229,167,234;
	 Disp("니플헤임필드",1,2); Pick("nif_fild");
F21: Restrict("Pre-RE",5,11);
	 setarray @c[2],158,206,151,219,205,148,186,247,134,204,193,235,200,177,137,189,201,224,160,205,194,150;
	 Disp("페이욘숲",1,11); Pick("pay_fild");
F22: setarray @c[0],208,227,190,206,240,206,190,143,307,252,239,213,185,188,193,194,187,218,210,183,195,149,198,164;
	 Disp("프론테라필드",0,11); Pick("prt_fild",1);
F23: Restrict("Pre-RE",2,7,9,10,11,13);
	 setarray @c[2],192,162,235,166,202,206,202,208,225,202,202,214,263,196,217,201,87,121,277,181,221,185,175,200,174,197;
	 Disp("라헬필드",1,13); Pick("ra_fild");
F24: setarray @c[2],219,205,177,206,194,182,224,170,198,216,156,187,185,263,206,228,208,238,209,223,85,97,207,202,31,195,38,195;
	 Disp("소그라트사막 1:소그라트사막 2:소그라트사막 3:소그라트사막 7:소그라트사막 11:소그라트사막 12:소그라트사막 13:소그라트사막 16:소그라트사막 17:소그라트사막 18:소그라트사막 19:소그라트사막 20:소그라트사막 21:소그라트사막 22");
	 Pick("","moc_fild01","moc_fild02","moc_fild03","moc_fild07","moc_fild11","moc_fild12","moc_fild13","moc_fild16","moc_fild17","moc_fild18","moc_fild19","moc_fild20","moc_fild21","moc_fild22");
F25: setarray @c[2],175,186,236,184,188,204;
	 Disp("스플랑디드필드",1,3); Pick("spl_fild");
F26: setarray @c[2],217,206,223,221,237,215,202,197;
	 Disp("움발라필드",1,4); Pick("um_fild");
F27: Restrict("Pre-RE",5);
	 setarray @c[2],186,175,196,370,222,45,51,250,202,324,150,223,149,307;
	 Disp("베인스필드",1,7); Pick("ve_fild");
F28: Restrict("Pre-RE",5,10);
	 setarray @c[2],189,224,192,207,221,157,226,199,223,177,187,232,231,174,196,203,183,214,200,124,195,226,210,304;
	 Disp("유노필드",1,12); Pick("yuno_fild");
F29: Restrict("RE");
	setarray @c[2],341,370,342,243;
	Disp("라자냐필드",1,2); Pick("lasa_fild");

// --------------------------------------------------
	Dungeons:
// --------------------------------------------------
menu	"거북섬",D39, "개미지옥",D3, "게페니아",D15, "게펜타워",D16, "곤륜던전",D18, 
	"글래스트헤임",D17, "노그로드",D24, "니드호그 던전",D27, "데와타 던전",D13,
	"라헬 신전",D32, "뤄양던전",D23, "말랑도던전",D25, "모스코비아 던전",D26,
	"묘르닐 폐광",D10, "무명섬 수도원",D12, "미궁",D19, "브라질리아 던전",D7, 
	"비프로스트 타워",D41, "생체 실험 연구소",D6, "스카라바 홀",D33, "스핑크스",D34, "시계탑",D9, "아마쯔 던전",D2, 
    	"아요타야던전",D4, "아인브로크 던전",D14, "알베르타 침몰선",D35, "어비스 지하동굴",D1, 
	"얼음던전",D20, "오딘 신전",D28, "오크던전",D29,"용의 둥지",D42,"움발라던전",D40,"유페로스",D21, 
	"이즈루드 던전",D8, "장난감공장",D38,"지하수로",D11,  
    	"코모도 던전",D5, "키엘던전",D22, "타나토스타워",D36,"토르 화산던전",D37, 
	"페이욘 던전",D30, "피라미드",D31;

D1: setarray @c[2],261,272,275,270,116,27;
	Disp("어비스 지하동굴",1,3); Pick("abyss_");
D2: setarray @c[2],228,11,34,41,119,14;
	Disp("아마쯔던전",1,3); Pick("ama_dun");
D3: setarray @c[2],35,262,168,170;
	Disp("개미지옥",1,2); Pick("anthell");
D4: setarray @c[2],275,19,24,26;
	Disp("아요타야고대신전 미궁:아요타야 고대신전 내부"); Pick("ayo_dun");
D5: setarray @c[2],266,67,255,244,23,260;
	Disp("코모도 서쪽 동굴 카루:코모도 북쪽 동굴 루안다:코모도 동쪽 동굴 마오");
	Pick("","beach_dun","beach_dun2","beach_dun3");
	Disp("코모도 던전",1,3); Pick("","beach_dun","beach_dun2","beach_dun3");
D6: Restrict("RE",4);
	setarray @c[2],150,288,150,18,140,134,244,52,98,202;
	Disp("생체실험 연구소 1:생체실험 연구소 2:생체실험 연구소 3:생체실험 연구소 4:전사자의 무덤");
	Pick("","lhz_dun01","lhz_dun02","lhz_dun03","lhz_dun04","lhz_dun_n");
	//Disp("생체실험 연구소",1,4); Pick("lhz_dun");
D7: Restrict("RE");
	setarray @c[2],87,47,262,262;
	Disp("브라질리아 던전",1,2); Pick("bra_dun");
D8: Restrict("RE",6);
	setarray @c[0],168,168,253,252,236,204,32,63,26,27,141,187;
	Disp("이즈루드 해저동굴",1,6); Pick("iz_dun",1);
D9: setarray @c[2],199,159,148,283,65,147,56,155,297,25,127,169,277,178,268,74,24,31,43,41;
	Disp("시계탑 1:시계탑 2:시계탑 3:시계탑 4:시계탑 지하 1:시계탑 지하 2:시계탑 지하 3:시계탑 지하 4:시계탑 2 나이트메어:시계탑 3 나이트메어");
	Pick("","c_tower1","c_tower2","c_tower3","c_tower4","alde_dun01","alde_dun02","alde_dun03","alde_dun04","c_tower2_","c_tower3_");
D10: setarray @c[2],52,17,381,343,302,262;
	 Disp("묘르닐 폐광",1,3); Pick("mjo_dun");
D11: setarray @c[2],131,247,19,19,180,169,100,92;
	 Disp("지하수로",1,4); Pick("","prt_sewb1","prt_sewb2","prt_sewb3","prt_sewb4");
D12: setarray @c[2],51,14,150,11,120,10;
	 Disp("이름없는섬 수도원 지하",1,3); Pick("abbey");
D13: Restrict("RE");
	 setarray @c[2],285,160,299,29;
	 Disp("데와타 던전",1,2); Pick("dew_dun");
D14: setarray @c[2],22,14,292,290;
	 Disp("아이브로크 던전",1,2); Pick("ein_dun");
D15: setarray @c[2],40,103,203,34,266,168,130,272;
	 Disp("게페니아",1,4); Pick("gefenia",0);
D16: setarray @c[0],104,99,115,236,106,132,203,200;
	 Disp("게펜던전",1,4); Pick("gef_dun",1);
D17: setarray @c[2],370,304,199,29,104,25,150,15,157,287,147,15,258,255,108,291,171,283,68,277,156,7,12,7,133,271,224,274,14,70,150,14,148,21,103,22;
	 Disp("입구:성 1:성 2:기사단 1:기사단 2:지하묘지:지하수로 1:지하수로 2:지하수로 3:지하수로 4:수도원:계단던전:지하동굴 1:지하동굴 2:지하감옥 1:지하감옥 2:지하감옥 나이트메어:성 2 나이트메어");
	 Pick("","glast_01","gl_cas01","gl_cas02","gl_knt01","gl_knt02","gl_chyard","gl_sew01","gl_sew02","gl_sew03","gl_sew04","gl_church","gl_step","gl_dun01","gl_dun02","gl_prison","gl_prison1","gl_chyard_", "gl_cas02_");
D18: setarray @c[2],153,53,28,113,68,16;
	 Disp("곤륜던전",1,3); Pick("gon_dun");
D19: setarray @c[2],176,7,93,20,23,8;
	 Disp("미궁",1,3); Pick("prt_maze");
D20: setarray @c[2],157,14,151,155,149,22,33,158;
	 Disp("얼음동굴",1,4); Pick("ice_dun");
D21: setarray @c[2],140,51,53,247,37,63,150,285;
	 Disp("입구:유페로스 1:유페로스 2:유페로스 중심부");
	 Pick("","jupe_cave","juperos_01","juperos_02","jupe_core");
D22: setarray @c[2],28,226,41,198;
	 Disp("Kiel Dungeon",1,2); Pick("kh_dun");
D23: setarray @c[2],218,196,282,20,165,38;
	 Disp("용지성 왕릉:용지성왕릉:센양궁"); Pick("lou_dun");
D24: setarray @c[2],126,68,47,30;
	 Disp("노그로드",1,2); Pick("mag_dun");
D25: Restrict("RE");
	 setarray @c[2],33,230;
	 Disp("말랑도 던전",1,1); Pick("mal_dun");
D26: setarray @c[2],189,48,165,30,32,135;
	 Disp("모스코비아 던전",1,3); Pick("mosk_dun");
D27: setarray @c[2],61,239,60,271;
	 Disp("니드호그 던전",1,2); Pick("nyd_dun");
D28: setarray @c[2],298,167,224,149,266,280;
	 Disp("오딘 신전",1,3); Pick("odin_tem");
D29: setarray @c[2],32,170,21,185;
	 Disp("오크 던전",1,2); Pick("orcsdun");
D30: setarray @c[0],21,183,19,33,19,63,155,159,201,204;
	 Disp("페이욘 던전",1,5); Pick("pay_dun",1);
D31: Restrict("RE",7,8);
	 setarray @c[2],192,9,10,192,100,92,181,11,94,96,192,8,94,96,192,8;
	 Disp("피라미드 1:피라미드 2:피라미드 3:피라미드 4:지하 1:지하 2:지하 1 - 나이트메어:지하 2 - 나이트메어");
	 Pick("","moc_pryd01","moc_pryd02","moc_pryd03","moc_pryd04","moc_pryd05","moc_pryd06","moc_prydn1","moc_prydn2");
D32: setarray @c[2],140,11,32,21,4,149,204,218,150,9;
	 Disp("라헬 신전",1,5); Pick("ra_san");
D33: Restrict("RE");
	 setarray @c[2],364,44,101,141,101,142;
	 Disp("스카라바 홀 1:스카라바 홀 2:스카라바 홀 나이트메어"); Pick("","dic_dun01","dic_dun02","dic_dun03");
D34: setarray @c[2],288,9,149,81,210,54,10,222,100,99;
	 Disp("스핑크스",1,5); Pick("","in_sphinx1","in_sphinx2","in_sphinx3","in_sphinx4","in_sphinx5");
D35: setarray @c[2],69,24,102,27;
	 Disp("알베르타 침몰선",1,2); Pick("treasure");
D36: setarray @c[2],150,39,150,136,220,158,59,143,62,11,89,221,35,166,93,148,29,107,159,138,19,20,130,52;
	 Disp("타나토스 타워",1,12); Pick("tha_t");
D37: setarray @c[2],21,228,75,205,34,272;
	 Disp("토르 화산던전",1,3); Pick("thor_v");
D38: setarray @c[2],205,15,129,133;
	 Disp("장난감 공장",1,2); Pick("xmas_dun");
D39: setarray @c[2],154,49,148,261,132,189,100,192;
	 Disp("입구:거북섬 던전 1:거북섬 던전 2:거북섬 던전 3"); Pick("tur_dun");
D40: Restrict("Pre-RE",1,2);
	 setarray @c[2],42,31,48,30,204,78;
	 Disp("움발라 나무속의 목공소:움발라 나무속의 이계통로:흐베르겔미르의 샘");
	 Pick("","um_dun01","um_dun02","yggdrasil01");
D41: Restrict("RE");
	 setarray @c[2],57,13,64,88,45,14,26,23;
	 Disp("비프로스트 타워",1,4); Pick("ecl_tdun");
D42: Restrict("RE");
	 setarray @c[2],24,143,146,54,190,19;
	 Disp("용의 둥지",1,3); Pick("lasa_dun");

// --------------------------------------------------
	Castles:
// --------------------------------------------------
menu	"알데바란성",C1, "게펜성",C2, "페이욘성",C3,
    	"프론테라성",C4, "아루나펠츠 성",C5, "슈발츠발드 성",C6;

C1: setarray @c[2],48,83,95,249,142,85,239,242,264,90;
	Disp("Neuschwanstein:Hohenschwangau:Nuenberg:Wuerzburg:Rothenburg");
	Pick("","alde_gld","alde_gld","alde_gld","alde_gld","alde_gld");
C2: setarray @c[2],214,75,308,240,143,240,193,278,305,87;
	Disp("Repherion:Eeyolbriggar:Yesnelph:Bergel:Mersetzdeitz");
	Pick("","gef_fild13","gef_fild13","gef_fild13","gef_fild13","gef_fild13");
C3: setarray @c[2],121,233,295,116,317,293,140,160,204,266;
	Disp("Bright Arbor:Scarlet Palace:Holy Shadow:Sacred Altar:Bamboo Grove Hill");
	Pick("","pay_gld","pay_gld","pay_gld","pay_gld","pay_gld");
C4: setarray @c[2],134,65,240,128,153,137,111,240,208,240;
	Disp("크림힐트:스반힐트:Fadhgridh:Skoegul:Gondul");
	Pick("","prt_gld","prt_gld","prt_gld","prt_gld","prt_gld");
C5: setarray @c[2],158,272,83,47,68,155,299,345,292,107;
	Disp("Mardol:Cyr:Horn:Gefn:Banadis");
	Pick("","aru_gld","aru_gld","aru_gld","aru_gld","aru_gld");
C6: setarray @c[2],293,100,288,252,97,196,137,90,71,315;
	Disp("Himinn:Andlangr:Viblainn:Hljod:Skidbladnir");
	Pick("","sch_gld","sch_gld","sch_gld","sch_gld","sch_gld");

// --------------------------------------------------
	Guild_Dungeons:
// --------------------------------------------------
menu	"발두르",G1, "루이나",G2, "발키리",G3, "브리타니아",G4,
    	"아루나펠츠",G5, "슈발츠발드",G6;

G1: Restrict("RE",2,3);
	setarray @c[2],119,93,119,93,120,130;
	Disp("발두르 F1:발두르 F2:심연의 회랑");
	Pick("","gld_dun01","gld_dun01_2","gld2_pay");
G2: Restrict("RE",2,3);
	setarray @c[2],39,161,39,161,147,155;
	Disp("루이나 F1:루이나 F2:심연의 회랑");
	Pick("","gld_dun02","gld_dun02_2","gld2_ald");
G3: Restrict("RE",2,3);
	setarray @c[2],50,44,50,44,140,132;
	Disp("발키리 F1:발키리 F2:심연의 회랑");
	Pick("","gld_dun03","gld_dun03_2","gld2_prt");
G4: Restrict("RE",2,3);
	setarray @c[2],116,45,116,45,152,118;
	Disp("브리타니아 F1:브리타니아 F2:심연의 회랑");
	Pick("","gld_dun04","gld_dun04_2","gld2_gef");
G5: Go("arug_dun01",199,195);
G6: Go("schg_dun01",200,124);

// --------------------------------------------------
	Instances:
// --------------------------------------------------
mes "※ 표기된 숫자는 공략 가능 레벨이 아닌 입장 가능 레벨입니다.";
menu	"비공정 습격 [125~]",I1,"버코너워 호수 [140~]",I2, "벙웅곳 병원 [100~]",I3, "부워여 동굴 [130~]",I4,"중앙 실험실 [140~]",I5,
    	"위기의 찰스턴 [130~]",I6,"마신의 탑 [130~]",I7,"엔들리스 타워 [80~]",I9,"페이스웜의 둥지 [140~]",I10,
	"게펜 마법대회 [90~]",I11,"망령의 왕궁 [120~]",I12,"호러 장난감 공장 [140~]",I14,"무한의 공간 [100~]",I15,
    	"마지막 방 [150~]",I16,"지하 배수로 [90~]",I17, "모로스 동굴 [160~]",I18, "니드호그의 둥지 [100~]",I19,"악몽의 지터벅 [120~]",I20,
	"문어 동굴 [60~]",I21,"옛 글레스트헤임 [130~]",I22,"오크의 기억 [60~]",I23,"의식의 방 [100~]",I24,"펜릴과 사라 [145~]",I25,
	"사라의 기억 [100~]",I26,"공중요새 침공 [145~]",I28,"보르세보 연구실 [100~]",I29;

//"에클라쥬 내부",I8,
//I8: Restrict("RE");
//	Go("ecl_hub01",128,12);
//"안개 숲 미궁 ",I13,
//I13: Restrict("RE");
//	Go("bif_fild01",161,334);
//"바포메트 봉인 신전",I27,
//I27: Go("monk_test",306,143);

I1: Restrict("RE");
	Go("dali02",133,82);
I2: Restrict("RE");
	Go("ma_scene01",172,175);
I3: Restrict("RE");
	Go("ma_dun01",151,8);
I4: Restrict("RE");
	Go("ma_fild02",316,317);
I5: Restrict("RE");
	Go("verus01",151,151);
I6: Restrict("RE");
	Go("verus04",77,117);
I7: Restrict("RE");
	Go("dali02",135,114);
I9: Go("e_tower",72,112);
I10: Restrict("RE");
	Go("dali",87,65);
I11: Restrict("RE");
	Go("dali",92,141);
I12: Restrict("RE");
	Go("dali02",48,127);
I14: Go("xmas",237,303);
I15: Go("cmd_fild07",52,277);
I16: Restrict("RE");
	Go("un_myst",158,38);
I17: Restrict("RE");
	Go("mal_in01",164,21);
I18: Restrict("RE");
	Go("moro_cav",59,62);
I19: Go("nyd_dun02",95,193);
I20: Restrict("RE");
	Go("moc_para01",27,90);
I21: Restrict("RE");
	Go("mal_dun01",152,230);
I22: Restrict("RE");
	Go("glast_01",204,268);
I23: Go("gef_fild10",240,198);
I24: Restrict("RE");
	Go("prt_lib_q",88,82);
I25: Restrict("RE");
	Go("dali02",93,141);
I26: Restrict("RE");
	Go("dali",135,112);
I28: Restrict("RE");
	Go("prt_q",248,79);
I29: Restrict("RE");
	Go("lighthalzen",306,299);


// --------------------------------------------------
	Special:
// --------------------------------------------------
menu	"룬 재료",S1, "화염병 & 염산병 재료",S2,"독약병 재료",S3;

S1: Go("pvp_n_1-3",100,103);
S2: Go("pvp_n_2-2",100,100);
S3: Go("pvp_n_2-3",100,103);

}

// --------------------------------------------------
//	Duplicates:
// --------------------------------------------------
prt_gld,171,116,4	duplicate(Warper)	워프#tre	627
//alberta,28,240,4	duplicate(Warper)	워프엔피시#alb	811
//aldebaran,145,118,4	duplicate(Warper)	워프엔피시#alde	811
//amatsu,203,87,4	duplicate(Warper)	워프엔피시#ama	811
//ayothaya,209,169,6	duplicate(Warper)	워프엔피시#ayo	811
//comodo,194,158,4	duplicate(Warper)	워프엔피시#com	811
//einbech,59,38,6	duplicate(Warper)	워프엔피시#einbe	811
//einbroch,69,202,4	duplicate(Warper)	워프엔피시#einbr	811
//gef_fild10,71,339,4	duplicate(Warper)	워프엔피시#orc	811
//geffen,124,72,4	duplicate(Warper)	워프엔피시#gef	811
//glast_01,372,308,4	duplicate(Warper)	워프엔피시#glh	811
//gonryun,162,122,4	duplicate(Warper)	워프엔피시#gon	811
//hugel,101,151,4	duplicate(Warper)	워프엔피시#hug	811
//izlu2dun,110,92,4	duplicate(Warper)	워프엔피시#izd	811
//izlude,134,150,4	duplicate(Warper)	워프엔피시#izl	811	//Pre-RE: (132,120)
//jawaii,253,138,4	duplicate(Warper)	워프엔피시#jaw	811
//lighthalzen,162,102,4	duplicate(Warper)	워프엔피시#lhz	811
//louyang,208,103,6	duplicate(Warper)	워프엔피시#lou	811
//manuk,274,146,6	duplicate(Warper)	워프엔피시#man	811
//mid_camp,216,288,4	duplicate(Warper)	워프엔피시#mid	811
//mjolnir_02,85,364,4	duplicate(Warper)	워프엔피시#mjo	811
//moc_ruins,64,164,4	duplicate(Warper)	워프엔피시#moc	811
//morocc,159,97,4	duplicate(Warper)	워프엔피시#mor	811
//moscovia,229,191,4	duplicate(Warper)	워프엔피시#mos	811
//nameless_n,259,213,4	duplicate(Warper)	워프엔피시#nam	811
//niflheim,205,179,4	duplicate(Warper)	워프엔피시#nif	811
//pay_arche,42,134,4	duplicate(Warper)	워프엔피시#arc	811
//payon,182,108,4	duplicate(Warper)	워프엔피시#pay	811
//prontera,161,176,4	duplicate(Warper)	워프엔피시#prt	811
//prt_fild05,279,223,6	duplicate(Warper)	워프엔피시#cul	811
//rachel,135,116,4	duplicate(Warper)	워프엔피시#rac	811
//splendide,205,153,4	duplicate(Warper)	워프엔피시#spl	811
//thor_camp,249,76,4	duplicate(Warper)	워프엔피시#thor	811
//umbala,106,150,3	duplicate(Warper)	워프엔피시#umb	811
//veins,214,123,4	duplicate(Warper)	워프엔피시#ve	811
//xmas,150,136,6	duplicate(Warper)	워프엔피시#xmas	811
//yuno,162,47,4	duplicate(Warper)	워프엔피시#yuno	811

// --------------------------------------------------
//	Duplicates (Renewal):
// --------------------------------------------------
//brasilis,201,222,4	duplicate(Warper)	워프엔피시#bra	811
//dewata,204,186,6	duplicate(Warper)	워프엔피시#dew	811
//dicastes01,194,194,6	duplicate(Warper)	워프엔피시#dic	811
//ecl_in01,51,60,4	duplicate(Warper)	워프엔피시#ecl	811
//malangdo,134,117,6	duplicate(Warper)	워프엔피시#mal	811
//malaya,231,204,4	duplicate(Warper)	워프엔피시#ma	811
//mora,57,152,4	duplicate(Warper)	워프엔피시#mora	811
